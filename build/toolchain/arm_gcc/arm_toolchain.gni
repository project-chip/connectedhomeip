# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/pigweed.gni")
import("$dir_pw_toolchain/generate_toolchain.gni")

template("arm_toolchain") {
  _arm_gcc_toolchain_tools = {
    _tool_name_root = "arm-none-eabi-"
    ar = _tool_name_root + "ar"
    cc = _tool_name_root + "gcc"
    cxx = _tool_name_root + "g++"
  }

  _toolchain = {
    name = target_name
    forward_variables_from(_arm_gcc_toolchain_tools, "*")
    defaults = {
      current_cpu = "arm"
      current_os = invoker.current_os

      if (defined(invoker.toolchain_args_file)) {
        import(invoker.toolchain_args_file)
      } else if (defined(invoker.defaults)) {
        forward_variables_from(invoker.defaults, "*")
      }
    }
  }

  generate_toolchains("host_toolchains") {
    toolchains = [ _toolchain ]
  }
}
