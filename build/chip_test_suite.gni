# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/tests.gni")

assert(chip_build_tests)

template("chip_test_suite") {
  suite_name = target_name

  static_library("${suite_name}_common") {
    forward_variables_from(invoker,
                           [
                             "deps",
                             "public_deps",
                           ])
    if (defined(invoker.common_sources)) {
      sources = invoker.common_sources
    }
  }

  tests = []
  foreach(test, invoker.tests) {
    executable(test) {
      sources = [ "${test}Driver.cpp" ]

      public_deps = [ ":${suite_name}_common" ]
    }

    tests += [ ":${test}" ]
  }

  group(suite_name) {
    deps = tests
  }
}
