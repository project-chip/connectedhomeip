# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/chip.gni")

import("${chip_root}/build/chip/tests.gni")

template("chip_build") {
  _build_name = target_name
  _toolchain = invoker.toolchain

  if (defined(invoker.test_group) && chip_build_tests) {
    _build_target = invoker.test_group
    _check_target = get_label_info(invoker.test_group, "dir") + ":" +
                    get_label_info(invoker.test_group, "name") + "_run"
  } else {
    _build_target = ":default"
    _check_target = ":check"
    not_needed(invoker, [ "test_group" ])
  }

  group("${_build_name}") {
    deps = [ "${_build_target}(${_toolchain})" ]
  }

  group("check_${_build_name}") {
    deps = [ "${_check_target}(${_toolchain})" ]
  }
}
