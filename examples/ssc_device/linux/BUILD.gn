# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/build.gni")
import("//build_overrides/chip.gni")

import("${build_root}/chip/tools.gni")
import("${chip_root}/src/app/common_flags.gni")

assert(chip_build_tools)

config("includes") {
  include_dirs = [
    ".",
    "include",
    "${chip_root}/src/app/util",
    "${chip_root}/src/app/server",
  ]
}
copy("libcertifier_cfg_copy") {
  sources = [ "${chip_root}/third_party/certifier/libcertifier.cfg" ]
  outputs = [ "$root_out_dir/libcertifier.cfg" ]
}

copy("libcertifier_cert_copy") {
  sources = [ "${chip_root}/third_party/certifier/libcertifier-cert.crt" ]
  outputs = [ "$root_out_dir/libcertifier-cert.crt" ]
}

executable("chip-tool-server") {
  sources = [
    "LightingManager.cpp",
    "Options.cpp",
    "include/LightingManager.h",
    "main.cpp",
  ]

  if (is_debug) {
    defines = [
      "BUILD_RELEASE=0",
      "CHIP_LOG_FILTERING=0",
    ]
  } else {
    defines = [
      "BUILD_RELEASE=1",
      "CHIP_LOG_FILTERING=1",
    ]
  }

  if (chip_app_use_interaction_model) {
    defines += [ "CHIP_APP_USE_INTERACTION_MODEL" ]
  }

  public_configs = [ ":includes" ]

  deps = [
    ":libcertifier_cert_copy",
    ":libcertifier_cfg_copy",
    "${chip_root}/examples/lighting-app/lighting-common",
    "${chip_root}/src/lib",
  ]

  output_dir = root_out_dir
}

group("linux") {
  deps = [ ":chip-tool-server" ]
}
