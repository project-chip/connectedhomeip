# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/chip.gni")
import("${chip_root}/examples/common/pigweed/pigweed_rpcs.gni")
import("${chip_root}/src/app/common_flags.gni")
import("${chip_root}/src/crypto/crypto.gni")
import("${chip_root}/src/lib/core/core.gni")
import("${chip_root}/src/lib/lib.gni")

config("app-main-config") {
  include_dirs = [ "." ]
}

source_set("ota-test-event-trigger") {
  sources = [
    "${chip_root}/src/app/clusters/ota-requestor/OTATestEventTriggerHandler.h",
  ]
}

source_set("app-main") {
  defines = []
  sources = [
    "${chip_root}/examples/platform/linux/AppMain.h",
    "${chip_root}/examples/platform/linux/CommissionableInit.cpp",
    "${chip_root}/examples/platform/linux/CommissionableInit.h",
    "${chip_root}/examples/platform/linux/CommissioneeShellCommands.cpp",
    "${chip_root}/examples/platform/linux/CommissioneeShellCommands.h",
    "${chip_root}/examples/platform/linux/CommissionerMain.cpp",
    "${chip_root}/examples/platform/linux/CommissionerMain.h",
    "${chip_root}/examples/platform/linux/ControllerShellCommands.cpp",
    "${chip_root}/examples/platform/linux/ControllerShellCommands.h",
    "${chip_root}/examples/platform/linux/LinuxCommissionableDataProvider.cpp",
    "${chip_root}/examples/platform/linux/LinuxCommissionableDataProvider.h",
    "${chip_root}/examples/platform/linux/NamedPipeCommands.cpp",
    "${chip_root}/examples/platform/linux/NamedPipeCommands.h",
    "${chip_root}/examples/platform/linux/Options.cpp",
    "${chip_root}/examples/platform/linux/Options.h",
    "${chip_root}/examples/platform/linux/testing/CustomCSRResponse.cpp",
    "${chip_root}/examples/platform/linux/testing/CustomCSRResponse.h",
    "${chip_root}/examples/platform/linux/testing/CustomCSRResponseOperationalKeyStore.cpp",
    "${chip_root}/examples/platform/linux/testing/CustomCSRResponseOperationalKeyStore.h",
    "${chip_root}/examples/platform/nxp/se05x/DeviceAttestationSe05xCredsExample.cpp",
    "${chip_root}/examples/platform/nxp/se05x/DeviceAttestationSe05xCredsExample_v2.cpp",
    "AppMain.cpp",
  ]

  include_dirs = [
    "${chip_root}/examples/platform/linux",
    "${chip_root}/examples/platform/nxp/se05x",
    "${chip_root}/src/platform/nxp/crypto/se05x",
  ]

  defines = []

  if (chip_enable_pw_rpc) {
    defines += [ "PW_RPC_ENABLED" ]
  }
  if (chip_build_libshell) {
    defines += [ "ENABLE_CHIP_SHELL" ]
  }

  deps = [ "${chip_root}/src/setup_payload:onboarding-codes-utils" ]

  public_deps = [
    ":ota-test-event-trigger",
    "${chip_root}/examples/providers:device_info_provider",
    "${chip_root}/src/app/server",
    "${chip_root}/src/credentials:default_attestation_verifier",
    "${chip_root}/src/lib",
    "${chip_root}/src/lib/shell",
    "${chip_root}/src/lib/shell:shell_core",
  ]

  if (chip_enable_transport_trace) {
    public_deps +=
        [ "${chip_root}/examples/common/tracing:trace_handlers_decoder" ]
  }

  public_configs = [ ":app-main-config" ]
}

source_set("commissioner-main") {
  defines = []
  sources = [
    "${chip_root}/examples/platform/linux/CommissionerMain.cpp",
    "${chip_root}/examples/platform/linux/CommissionerMain.h",
  ]

  defines = []

  if (chip_enable_pw_rpc) {
    defines += [ "PW_RPC_ENABLED" ]
  }
  if (chip_build_libshell) {
    defines += [ "ENABLE_CHIP_SHELL" ]
  }

  public_deps = [
    "${chip_root}/src/app/server",
    "${chip_root}/src/credentials:default_attestation_verifier",
    "${chip_root}/src/lib",
  ]

  if (chip_enable_transport_trace) {
    public_deps += [ "${chip_root}/examples/common/tracing:trace_handlers" ]
  }

  public_configs = [ ":app-main-config" ]
}
