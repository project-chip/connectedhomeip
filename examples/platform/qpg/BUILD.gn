# SPDX-FileCopyrightText: 2021 Project CHIP Authors
#
# SPDX-License-Identifier: Apache-2.0

import("//build_overrides/chip.gni")
import("//build_overrides/qpg_sdk.gni")

import("${qpg_sdk_build_root}/qpg_sdk.gni")

# declares chip_build_libshell = false
import("${chip_root}/src/lib/lib.gni")
config("chip_examples_project_config") {
  include_dirs = [ "project_include" ]
}

source_set("openthread_core_config_qpg_chip_examples") {
  sources = [ "project_include/OpenThreadConfig.h" ]

  public_deps = [
    "${chip_root}/third_party/openthread/platforms/qpg:openthread_core_config_qpg",
    "${qpg_sdk_build_root}:qpg_sdk",
  ]

  public_configs = [ ":chip_examples_project_config" ]
}

source_set("qpg-matter-shell") {
  defines = []
  if (chip_build_libshell) {
    defines += [
      "OPENTHREAD_CONFIG_CLI_TRANSPORT=OT_CLI_TRANSPORT_CONSOLE",
      "ENABLE_CHIP_SHELL",
      "CHIP_DEVICE_CONFIG_THREAD_ENABLE_CLI=1",
    ]

    sources = [
      "shell_common/shell.cpp",
      "uart.c",
    ]

    include_dirs = [ "." ]

    public_deps = [
      "${chip_root}/examples/shell/shell_common:shell_common",
      "${chip_root}/src/lib/shell:shell",
      "${chip_root}/src/lib/shell:shell_core",
    ]
  }
}
