# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/chip.gni")
import("//build_overrides/nrf5_sdk.gni")
import("//build_overrides/openthread.gni")

import("${nrf5_sdk_build_root}/nrf5_sdk.gni")

assert(current_os == "freertos")

nrf5_platform_dir = "${chip_root}/examples/platform/nrf528xx"

nrf5_sdk("sdk") {
  include_dirs = [
    "main/include",
    "main",
    "${nrf5_platform_dir}/app/project_include",
    "${nrf5_platform_dir}/util/include",
    "${nrf5_platform_dir}/app/include",
    "${chip_root}/src/app/util",
  ]

  sources = [
    "${nrf5_platform_dir}/app/project_include/CHIPProjectConfig.h",
    "${nrf5_platform_dir}/app/project_include/FreeRTOSConfig.h",
    "${nrf5_platform_dir}/app/project_include/OpenThreadConfig.h",
    "${nrf5_platform_dir}/app/project_include/freertos_tasks_c_additions.h",
    "${nrf5_platform_dir}/app/project_include/nrf_log_ctrl_internal.h",
    "main/include/app_config.h",
  ]

  defines = []
  if (is_debug) {
    defines += [ "BUILD_RELEASE=0" ]
  } else {
    defines += [ "BUILD_RELEASE=1" ]
  }

  defines += [ "USE_APP_CONFIG" ]
}

# Convert a binary to a target format using objcopy.
# TODO: This is not specific to nrf5; it could be shared by any
#       similar toolchain. The `command_wrapper.py` script is
#       merely a workaround for GN's requirement that actions
#       run Python scripts rather than arbitrary programs.
#
template("objcopy_convert") {
  forward_variables_from(invoker,
                         [
                           "conversion_input",
                           "conversion_output",
                           "conversion_target_format",
                           "deps",
                           "objcopy",
                         ])

  action(target_name) {
    inputs = [ conversion_input ]
    outputs = [ conversion_output ]

    args = [
      objcopy,
      "-O",
      conversion_target_format,
      rebase_path(conversion_input, root_build_dir),
      rebase_path(conversion_output, root_build_dir),
    ]
    script = rebase_path("command_wrapper.py")
  }
}

# Build target for an executable, converted to the preferred form for flashing,
# plus a script that performs the flashing operation.
#
# The intent is that every flashable (or testable) build target in CHIP will
# ultimately be flashable/runnable in a consistent way — by a script as here,
# or by a common tool using consistently generated metadata — with platform
# and build-target details encapsulated.
#
# TODO:
#
# Currently all of this flashable_executable() is GN-target-independent but
# not toolchain- or platform-independent. Flash script generation could evolve
# to be platform-independent by having `make_flasher.py` be part of the platform.
# Objcopy could also be wrapped in a platform-specific script to encapsulate
# the target format.
#
# This is loosely inspired by Fuchsia's template("image_binary"). However,
# Fuchsia uses a wrapper around GN toolchains to deal with the problem of
# things like `objcopy` not being first-class tools in GN with composable
# configuration, and that does not seem compatible with Pigweed's toolchain
# utilities.

template("flashable_executable") {
  executable_target = "$target_name.executable"
  executable_name = invoker.output_name

  image_target = "$target_name.image"
  image_name = executable_name + ".hex"
  image_format = "ihex"

  flasher_target = target_name
  flasher_name = executable_name + ".flash.py"

  executable(executable_target) {
    forward_variables_from(invoker, "*")
  }

  objcopy_convert(image_target) {
    conversion_input = "${root_build_dir}/${executable_name}"
    conversion_output = "${root_build_dir}/${image_name}"
    conversion_target_format = image_format
    deps = [ ":$executable_target" ]

    # TODO: This shouldn't be here.
    objcopy = "arm-none-eabi-objcopy"
  }

  action(flasher_target) {
    flasher_script = "$root_build_dir/${flasher_name}"
    flasher_image = "${root_build_dir}/${image_name}"

    outputs = [ flasher_script ]
    deps = [ ":$image_target" ]

    args = [
      "--output",
      rebase_path(flasher_script, root_build_dir),
      "--image",
      rebase_path(flasher_image, root_build_dir),
    ]
    script = "make_flasher.py"
  }
}

flashable_executable("lock_app") {
  output_name = "chip-nrf52840-lock-example"

  sources = [
    "${chip_root}/src/app/clusters/on-off-server/on-off.c",
    "${chip_root}/src/app/util/attribute-size.c",
    "${chip_root}/src/app/util/attribute-storage.c",
    "${chip_root}/src/app/util/attribute-table.c",
    "${chip_root}/src/app/util/chip-response.cpp",
    "${chip_root}/src/app/util/client-api.c",
    "${chip_root}/src/app/util/ember-print.cpp",
    "${chip_root}/src/app/util/message.c",
    "${chip_root}/src/app/util/process-cluster-message.c",
    "${chip_root}/src/app/util/process-global-message.c",
    "${chip_root}/src/app/util/util.c",
    "${nrf5_platform_dir}/app/Server.cpp",
    "${nrf5_platform_dir}/app/chipinit.cpp",
    "${nrf5_platform_dir}/app/include/Server.h",
    "${nrf5_platform_dir}/app/include/chipinit.h",
    "${nrf5_platform_dir}/app/support/CXXExceptionStubs.cpp",
    "${nrf5_platform_dir}/app/support/nRF5Sbrk.c",
    "${nrf5_platform_dir}/util/LEDWidget.cpp",
    "${nrf5_platform_dir}/util/include/LEDWidget.h",
    "main/AppTask.cpp",
    "main/BoltLockManager.cpp",
    "main/DataModelHandler.cpp",
    "main/gen/call-command-handler.c",
    "main/gen/callback-stub.c",
    "main/gen/znet-bookkeeping.c",
    "main/include/AppEvent.h",
    "main/include/AppTask.h",
    "main/include/BoltLockManager.h",
    "main/include/DataModelHandler.h",
    "main/main.cpp",
  ]

  deps = [
    ":sdk",
    "${chip_root}/src/lib",
    "${chip_root}/src/setup_payload",
    "${chip_root}/third_party/openthread/platforms/nrf528xx:libnordicsemi_nrf52840_radio_driver_softdevice",
    "${chip_root}/third_party/openthread/platforms/nrf528xx:libopenthread-nrf52840-softdevice-sdk",
    "${nrf5_platform_dir}/app/support:freertos_debugging_hooks",
    "${nrf5_platform_dir}/app/support:freertos_newlib_lock_support",
    "${nrf5_platform_dir}/app/support:freertos_newlib_lock_support_test",
    "${openthread_root}:libopenthread-cli-ftd",
    "${openthread_root}:libopenthread-ftd",
  ]

  output_dir = root_out_dir

  ldscript = "${nrf5_platform_dir}/app/ldscripts/chip-nrf52840-example.ld"

  ldflags = [ "-T" + rebase_path(ldscript, root_build_dir) ]
}

group("nrf5") {
  deps = [ ":lock_app" ]
}

group("default") {
  deps = [ ":nrf5" ]
}
