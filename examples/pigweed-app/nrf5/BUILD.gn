# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/chip.gni")
import("//build_overrides/nrf5_sdk.gni")
import("//build_overrides/openthread.gni")
import("//build_overrides/pigweed.gni")
import("$dir_pw_build/target_types.gni")

import("${nrf5_sdk_build_root}/nrf5_executable.gni")
import("${nrf5_sdk_build_root}/nrf5_sdk.gni")

assert(current_os == "freertos")

nrf5_platform_dir = "${chip_root}/examples/platform/nrf528xx"

pw_static_library("hdlc_server") {
  public_configs = [ "${dir_pigweed}/pw_hdlc_lite:default_config" ]
  sources = [
    "${dir_pigweed}/pw_hdlc_lite/rpc_example/hdlc_rpc_server.cc",
    "main/include/app_config.h",
    "main/main.cpp",
  ]
  deps = [
    "$dir_pw_rpc:server",
    "$dir_pw_rpc/nanopb:echo_service",
    "${dir_pigweed}/pw_hdlc_lite:pw_rpc",
    "${nrf5_platform_dir}/app/support:freertos_debugging_hooks",
    "${nrf5_platform_dir}/app/support:freertos_newlib_lock_support",
    "${nrf5_platform_dir}/pw_sys_io:pw_sys_io_baremetal_nrf528xx",
    "${nrf5_platform_dir}/sdk:sdk",
    dir_pw_hdlc_lite,
    dir_pw_log,
  ]
}

nrf5_executable("pigweed_app") {
  output_name = "chip-nrf52840-pigweed-example"

  deps = [ ":hdlc_server" ]

  output_dir = root_out_dir

  ldscript = "${nrf5_platform_dir}/app/ldscripts/chip-nrf52840-example.ld"

  ldflags = [ "-T" + rebase_path(ldscript, root_build_dir) ]
}

copy("mobly_tests") {
  sources = [
    "mobly_tests/pigweed_mobly_config.yml",
    "mobly_tests/pigweed_mobly_test.py",
  ]
  outputs = [ "$root_out_dir/{{source_file_part}}" ]
}

group("nrf5") {
  deps = [
    ":mobly_tests",
    ":pigweed_app",
  ]
}

group("default") {
  deps = [ ":nrf5" ]
}
