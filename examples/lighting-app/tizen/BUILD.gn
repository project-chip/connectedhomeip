# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/chip.gni")
import("//build_overrides/tizen.gni")

import("${chip_root}/build/chip/tools.gni")
import("${chip_root}/src/app/common_flags.gni")
import("${tizen_sdk_build_root}/tizen_sdk.gni")

assert(chip_build_tools)

executable("chip-lighting-app") {
  sources = [
    "${root_out_dir}/lightapp-iface.c",
    "${root_out_dir}/lightapp-iface.h",
    "include/CHIPProjectAppConfig.h",
    "src/main.cpp",
  ]

  deps = [
    "//:iface",
    "${chip_root}/examples/lighting-app/lighting-common",
    "${chip_root}/examples/lighting-app/lighting-common:lighting-manager",
    "${chip_root}/examples/platform/tizen:app-main",
    "${chip_root}/src/lib",
  ]

  include_dirs = [ "include" ]

  output_dir = root_out_dir
}

tizen_sdk_package("chip-lighting-app:tpk") {
  deps = [
    ":chip-lighting-app",
    "${chip_root}/examples/platform/tizen:author-certificate-CHIP",
  ]
  manifest = "tizen-manifest.xml"
  sign_security_profile = "CHIP"
}

group("tizen") {
  deps = [ ":chip-lighting-app" ]
}

group("tizen:tpk") {
  deps = [ ":chip-lighting-app:tpk" ]
}

group("default") {
  deps = [ ":tizen" ]
}

# group("iface") {
action("iface") {
  script = "${build_root}/gn_run_binary.py"
  args = [
    "gdbus-codegen",
    "--interface-prefix=org.tizen.communication",
    "--pragma-once",
    "--c-namespace",
    "LightApp",
    "--c-generate-object-manager",
    "--generate-c-code",
    "lightapp-iface",
    "../../examples/lighting-app/tizen/xml/Manager1.xml",
  ]
  outputs = [
    "${root_out_dir}/lightapp-iface.h",
    "${root_out_dir}/lightapp-iface.c",
  ]
}
# }
