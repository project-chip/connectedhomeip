# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/chip.gni")
import("${chip_root}/examples/common/pigweed/pigweed_rpcs.gni")
import("${chip_root}/src/app/common_flags.gni")
import("${chip_root}/src/lib/core/core.gni")

if (chip_enable_pw_rpc) {
  import("//build_overrides/pigweed.gni")
  import("$dir_pw_build/python.gni")
  import("$dir_pw_protobuf_compiler/proto.gni")
}

config("includes") {
  include_dirs = [
    ".",
    "include",
  ]
}

if (chip_enable_pw_rpc) {
  pw_proto_library("lighting_service") {
    sources = [ "lighting_service/lighting_service.proto" ]
    deps = [ "${chip_root}/examples/common/pigweed:empty_proto" ]
  }
}

source_set("lighting-common") {
  sources = [
    "${chip_root}/src/app/clusters/bindings/bindings.cpp",
    "${chip_root}/src/app/clusters/level-control/level-control.cpp",
    "${chip_root}/src/app/clusters/network-commissioning/network-commissioning-ember.cpp",
    "${chip_root}/src/app/clusters/network-commissioning/network-commissioning.cpp",
    "${chip_root}/src/app/clusters/on-off-server/on-off.cpp",
    "${chip_root}/src/app/reporting/reporting-default-configuration.cpp",
    "${chip_root}/src/app/reporting/reporting.cpp",
    "${chip_root}/src/app/server/DataModelHandler.cpp",
    "${chip_root}/src/app/util/af-event.cpp",
    "${chip_root}/src/app/util/af-main-common.cpp",
    "${chip_root}/src/app/util/attribute-size.cpp",
    "${chip_root}/src/app/util/attribute-storage.cpp",
    "${chip_root}/src/app/util/attribute-table.cpp",
    "${chip_root}/src/app/util/binding-table.cpp",
    "${chip_root}/src/app/util/chip-message-send.cpp",
    "${chip_root}/src/app/util/client-api.cpp",
    "${chip_root}/src/app/util/ember-print.cpp",
    "${chip_root}/src/app/util/message.cpp",
    "${chip_root}/src/app/util/process-cluster-message.cpp",
    "${chip_root}/src/app/util/process-global-message.cpp",
    "${chip_root}/src/app/util/util.cpp",
    "gen/call-command-handler.cpp",
    "gen/callback-stub.cpp",
  ]

  if (chip_enable_interaction_model) {
    sources += [
      "${chip_root}/src/app/util/ember-compatibility-functions.cpp",
      "gen/IMClusterCommandHandler.cpp",
    ]
  }

  deps = [ "${chip_root}/src/lib" ]

  public_configs = [ ":includes" ]
}
