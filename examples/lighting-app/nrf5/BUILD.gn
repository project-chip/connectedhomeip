# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/chip.gni")
import("//build_overrides/nrf5_sdk.gni")

import("${nrf5_sdk_build_root}/nrf5_sdk.gni")

assert(current_os == "freertos")

nrf5_platform_dir = "${chip_root}/examples/platform/nrf528xx"

nrf5_sdk("sdk") {
  include_dirs = [
    "main/include",
    "main",
    "${nrf5_platform_dir}/app/project_include",
    "${nrf5_platform_dir}/util/include",
    "${nrf5_platform_dir}/app/include",
  ]

  sources = [
    "${nrf5_platform_dir}/app/project_include/CHIPProjectConfig.h",
    "${nrf5_platform_dir}/app/project_include/FreeRTOSConfig.h",
    "${nrf5_platform_dir}/app/project_include/OpenThreadConfig.h",
    "${nrf5_platform_dir}/app/project_include/freertos_tasks_c_additions.h",
    "${nrf5_platform_dir}/app/project_include/nrf_log_ctrl_internal.h",
    "main/include/app_config.h",
  ]

  defines = []
  if (is_debug) {
    defines += [ "BUILD_RELEASE=0" ]
  } else {
    defines += [ "BUILD_RELEASE=1" ]
  }

  defines += [ "USE_APP_CONFIG" ]
}

executable("lighting_app") {
  output_name = "chip-nrf52840-lighting-example"

  public_deps = [
    ":sdk",
    "${chip_root}/src/lib",
  ]

  sources = [
    "${nrf5_platform_dir}/app/Server.cpp",
    "${nrf5_platform_dir}/app/chipinit.cpp",
    "${nrf5_platform_dir}/app/include/Server.h",
    "${nrf5_platform_dir}/app/include/chipinit.h",
    "${nrf5_platform_dir}/app/support/CXXExceptionStubs.cpp",
    "${nrf5_platform_dir}/app/support/FreeRTOSNewlibLockSupport.c",
    "${nrf5_platform_dir}/app/support/nRF5Sbrk.c",
    "${nrf5_platform_dir}/util/LEDWidget.cpp",
    "${nrf5_platform_dir}/util/include/LEDWidget.h",
    "main/AppTask.cpp",
    "main/DataModelHandler.cpp",
    "main/LightingManager.cpp",
    "main/include/AppEvent.h",
    "main/include/AppTask.h",
    "main/include/DataModelHandler.h",
    "main/include/LightingManager.h",
    "main/main.cpp",
  ]

  output_dir = root_out_dir

  ldscript = "${nrf5_platform_dir}/app/ldscripts/chip-nrf52840-example.ld"

  ldflags = [ "-T" + rebase_path(ldscript, root_build_dir) ]
}

group("nrf5") {
  deps = [ ":lighting_app" ]
}
