# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/defaults.gni")
import("//build_overrides/chip.gni")
import("//build_overrides/efr32_sdk.gni")
import("${efr32_sdk_build_root}/efr32_executable.gni")
import("${efr32_sdk_build_root}/efr32_sdk.gni")

assert(current_os == "freertos")

efr32_project_dir = "${chip_root}/examples/lighting-app/efr32"
examples_plat_dir = "${chip_root}/examples/platform/efr32"

declare_args() {
  # Dump memory usage at link time.
  chip_print_memory_usage = false
}

show_qr_code = true

# BRD4166A --> ThunderBoard Sense 2 (No LCD)
# BRD4180A --> GPIO Conflict with Radio and LCD
if (efr32_board == "BRD4166A" || efr32_board == "BRD4180A") {
  show_qr_code = false
}

efr32_sdk("sdk") {
  include_dirs = [
    "${chip_root}/src/platform/EFR32",
    "${efr32_project_dir}/include",
    "${efr32_project_dir}/src",
    "${examples_plat_dir}/${efr32_family}/${efr32_board}",
  ]

  sources = [
    "${efr32_project_dir}/include/CHIPProjectConfig.h",
    "${efr32_project_dir}/include/FreeRTOSConfig.h",
    "${examples_plat_dir}/${efr32_family}/${efr32_board}/hal-config.h",
  ]

  defines = []
  if (is_debug) {
    defines += [ "BUILD_RELEASE=0" ]
  } else {
    defines += [ "BUILD_RELEASE=1" ]
  }

  defines += [ "BOARD_ID=${efr32_board}" ]
}

efr32_executable("lighting_app") {
  include_dirs = []
  defines = []
  output_name = "chip-efr32-lighting-example.out"

  public_deps = [
    ":sdk",
    "${chip_root}/examples/common/chip-app-server:chip-app-server",
    "${chip_root}/src/lib",
    "${chip_root}/src/setup_payload",
    "${chip_root}/third_party/openthread/platforms:libopenthread-platform",
    "${chip_root}/third_party/openthread/platforms:libopenthread-platform-utils",
    "${chip_root}/third_party/openthread/repo:libopenthread-cli-ftd",
    "${chip_root}/third_party/openthread/repo:libopenthread-ftd",
  ]

  include_dirs += [
    "${efr32_project_dir}/include",
    "${chip_root}/src/app/util",
    "${examples_plat_dir}",
    "${chip_root}/examples/common/QRCode/repo/c",
  ]

  sources = [
    "${chip_root}/examples/common/QRCode/repo/c/qrcodegen.c",
    "${chip_root}/examples/common/chip-app-server/DataModelHandler.cpp",
    "${chip_root}/src/app/clusters/on-off-server/on-off.cpp",
    "${chip_root}/src/app/util/af-event.cpp",
    "${chip_root}/src/app/util/af-main-common.cpp",
    "${chip_root}/src/app/util/attribute-size.cpp",
    "${chip_root}/src/app/util/attribute-storage.cpp",
    "${chip_root}/src/app/util/attribute-table.cpp",
    "${chip_root}/src/app/util/binding-table.cpp",
    "${chip_root}/src/app/util/chip-message-send.cpp",
    "${chip_root}/src/app/util/client-api.cpp",
    "${chip_root}/src/app/util/ember-print.cpp",
    "${chip_root}/src/app/util/message.cpp",
    "${chip_root}/src/app/util/process-cluster-message.cpp",
    "${chip_root}/src/app/util/process-global-message.cpp",
    "${chip_root}/src/app/util/util.cpp",
    "${examples_plat_dir}/${efr32_family}/${efr32_board}/hal-config.h",
    "${examples_plat_dir}/${efr32_family}/${efr32_board}/init_board.c",
    "${examples_plat_dir}/${efr32_family}/${efr32_board}/init_mcu.c",
    "${examples_plat_dir}/LEDWidget.cpp",
    "${examples_plat_dir}/Service.cpp",
    "${examples_plat_dir}/init_otSystem.c",
    "src/AppTask.cpp",
    "src/ButtonHandler.cpp",
    "src/LightingManager.cpp",
    "src/ZclCallbacks.cpp",
    "src/gen/call-command-handler.c",
    "src/gen/callback-stub.c",
    "src/main.cpp",
  ]

  if (show_qr_code) {
    sources += [ "${examples_plat_dir}/display/lcd.c" ]

    defines += [ "DISPLAY_ENABLED" ]
  }
  output_dir = root_out_dir

  if (efr32_family == "efr32mg12") {
    ldscript = "${efr32_project_dir}/ldscripts/efr32-light-example-MG12P.ld"
  } else if (efr32_family == "efr32mg21") {
    ldscript = "${efr32_project_dir}/ldscripts/efr32-light-example-MG21.ld"
  }

  ldflags = [ "-T" + rebase_path(ldscript, root_build_dir) ]

  if (chip_print_memory_usage) {
    ldflags += [
      "-Wl,--print-memory-usage",
      "-fstack-usage",
    ]
  }
}

group("efr32") {
  deps = [ ":lighting_app" ]
}

group("default") {
  deps = [ ":efr32" ]
}
