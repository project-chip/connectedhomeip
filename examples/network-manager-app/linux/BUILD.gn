# Copyright (c) 2023 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/build.gni")
import("//build_overrides/chip.gni")

declare_args() {
  # Enable OpenWrt ubus integration
  matter_enable_ubus = false
}

executable("matter-network-manager-app") {
  sources = [
    "include/CHIPProjectAppConfig.h",
    "main.cpp",
  ]

  deps = [
    "${chip_root}/examples/network-manager-app/network-manager-common",
    "${chip_root}/examples/platform/linux:app-main",
    "${chip_root}/src/lib",
  ]

  libs = []
  defines = []
  include_dirs = [ "include" ]

  if (matter_enable_ubus) {
    defines += [ "MATTER_ENABLE_UBUS=1" ]
    sources += [
      "ThreadBROpenThreadUbus.cpp",
      "ThreadBROpenThreadUbus.h",
      "UboxUtils.cpp",
      "UboxUtils.h",
      "UbusManager.cpp",
      "UbusManager.h",
      "UloopHandler.cpp",
      "UloopHandler.h",
    ]
    libs += [
      "ubox",
      "ubus",
    ]
  } else {
    sources += [ "ThreadBRFake.h" ]
  }

  output_dir = root_out_dir
}

group("linux") {
  deps = [ ":matter-network-manager-app" ]
}

group("default") {
  deps = [ ":linux" ]
}
