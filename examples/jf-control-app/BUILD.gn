# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/build.gni")
import("//build_overrides/chip.gni")

import("${chip_root}/build/chip/tools.gni")
import("${chip_root}/examples/jf-control-app/jfc.gni")
import("${chip_root}/src/lib/core/core.gni")

if (config_use_interactive_mode) {
  import("//build_overrides/editline.gni")
}

import("${chip_root}/build_overrides/boringssl.gni")
import("${chip_root}/src/crypto/crypto.gni")

assert(chip_build_tools)

import("${chip_root}/examples/common/pigweed/pigweed_rpcs.gni")

if (chip_enable_pw_rpc) {
  import("//build_overrides/pigweed.gni")
  import("$dir_pw_build/target_types.gni")
}

config("config") {
  include_dirs = [
    ".",
    "${chip_root}/examples/common",
    "${chip_root}/examples/chip-tool",
    "${chip_root}/examples/chip-tool/commands/common",
    "${chip_root}/examples/platform/linux",
    "${chip_root}/zzz_generated/app-common/app-common",
    "${chip_root}/zzz_generated/chip-tool",
    "${chip_root}/src/lib",
  ]

  defines = [
    "CONFIG_USE_SEPARATE_EVENTLOOP=${config_use_separate_eventloop}",
    "CONFIG_USE_INTERACTIVE_MODE=${config_use_interactive_mode}",
  ]

  # Note: CONFIG_USE_LOCAL_STORAGE is tested for via #ifdef, not #if.
  if (config_use_local_storage) {
    defines += [ "CONFIG_USE_LOCAL_STORAGE" ]
  }

  if (config_enable_https_requests) {
    defines += [ "CONFIG_ENABLE_HTTPS_REQUESTS" ]
  }

  cflags = [ "-Wconversion" ]
}

static_library("jfc-utils") {
  sources = [
    "${chip_root}/examples/chip-tool/commands/clusters/ModelCommand.cpp",
    "${chip_root}/examples/chip-tool/commands/clusters/ModelCommand.h",
    "${chip_root}/examples/chip-tool/commands/common/BDXDiagnosticLogsServerDelegate.cpp",
    "${chip_root}/examples/chip-tool/commands/common/CHIPCommand.h",
    "${chip_root}/examples/chip-tool/commands/common/Command.cpp",
    "${chip_root}/examples/chip-tool/commands/common/Command.h",
    "${chip_root}/examples/chip-tool/commands/common/Commands.cpp",
    "${chip_root}/examples/chip-tool/commands/common/Commands.h",
    "${chip_root}/examples/chip-tool/commands/common/CredentialIssuerCommands.h",
    "${chip_root}/examples/chip-tool/commands/common/HexConversion.h",
    "${chip_root}/examples/chip-tool/commands/common/RemoteDataModelLogger.cpp",
    "${chip_root}/examples/chip-tool/commands/common/RemoteDataModelLogger.h",
    "${chip_root}/examples/chip-tool/commands/icd/ICDCommand.cpp",
    "${chip_root}/examples/chip-tool/commands/icd/ICDCommand.h",
    "${chip_root}/examples/chip-tool/commands/pairing/OpenCommissioningWindowCommand.cpp",
    "${chip_root}/examples/chip-tool/commands/pairing/OpenCommissioningWindowCommand.h",
    "${chip_root}/examples/chip-tool/commands/pairing/ToTLVCert.cpp",
    "${chip_root}/examples/platform/linux/RpcClientProcessor.cpp",
    "${chip_root}/examples/platform/linux/RpcClientProcessor.h",
    "${chip_root}/src/controller/ExamplePersistentStorage.cpp",
    "${chip_root}/src/controller/ExamplePersistentStorage.h",
    "${chip_root}/zzz_generated/chip-tool/zap-generated/cluster/ComplexArgumentParser.cpp",
    "${chip_root}/zzz_generated/chip-tool/zap-generated/cluster/logging/DataModelLogger.cpp",
    "${chip_root}/zzz_generated/chip-tool/zap-generated/cluster/logging/EntryToText.cpp",
    "commands/common/CHIPCommand.cpp",
    "commands/example/ExampleOperationalCredentialsIssuer.cpp",
    "commands/example/ExampleOperationalCredentialsIssuer.h",
    "commands/pairing/PairingCommand.cpp",
    "commands/pairing/PairingCommand.h",
  ]

  deps = [ "${chip_root}/src/app:events" ]

  if (config_use_interactive_mode) {
    sources += [ "${chip_root}/examples/chip-tool/commands/interactive/InteractiveCommands.cpp" ]
    deps += [
      "${chip_root}/examples/common/websocket-server",
      "${chip_root}/src/platform/logging:headers",
      "${editline_root}:editline",
    ]
  }

  if (chip_device_platform == "darwin") {
    sources += [ "commands/common/DeviceScanner.cpp" ]
  }

  if (chip_device_platform == "darwin" || chip_crypto == "boringssl") {
    deps += [ "${boringssl_root}:boringssl_with_ssl_sources" ]
  }

  public_deps = [
    "$dir_pw_hdlc:default_addresses",
    "$dir_pw_hdlc:rpc_channel_output",
    "$dir_pw_log",
    "$dir_pw_rpc:server",
    "$dir_pw_rpc/system_server:facade",
    "$dir_pw_rpc/system_server:socket",
    "$dir_pw_stream:socket_stream",
    "$dir_pw_stream:sys_io_stream",
    "$dir_pw_sync:mutex",
    "$dir_pw_trace",
    "$dir_pw_trace_tokenized",
    "$dir_pw_trace_tokenized:trace_rpc_service",
    "${chip_root}/config/linux/lib/pw_rpc:pw_rpc",
    "${chip_root}/examples/common/pigweed:joint_fabric_service.nanopb_rpc",
    "${chip_root}/examples/common/pigweed:rpc_services",
    "${chip_root}/examples/common/tracing:commandline",
    "${chip_root}/src/app/icd/client:handler",
    "${chip_root}/src/app/icd/client:manager",
    "${chip_root}/src/app/server",
    "${chip_root}/src/app/tests/suites/commands/interaction_model",
    "${chip_root}/src/controller/data_model",
    "${chip_root}/src/credentials:file_attestation_trust_store",
    "${chip_root}/src/credentials:test_dac_revocation_delegate",
    "${chip_root}/src/lib",
    "${chip_root}/src/lib/core:types",
    "${chip_root}/src/lib/support/jsontlv",
    "${chip_root}/src/platform",
    "${chip_root}/third_party/inipp",
    "${chip_root}/third_party/jsoncpp",
  ]

  public_deps += pw_build_LINK_DEPS

  public_configs = [ ":config" ]

  if (chip_enable_transport_trace) {
    public_deps +=
        [ "${chip_root}/examples/common/tracing:trace_handlers_decoder" ]
  }

  output_dir = root_out_dir
}

executable("jfc-app") {
  sources = [ "main.cpp" ]

  deps = [
    ":jfc-utils",
    "${chip_root}/src/platform/logging:stdio",
  ]

  output_dir = root_out_dir
}

group("default") {
  deps = [ ":jfc-app" ]
}
