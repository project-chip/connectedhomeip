# Copyright (c) 2022 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/build.gni")
import("//build_overrides/chip.gni")

import("${chip_root}/build/chip/tools.gni")
import("${chip_root}/examples//chip-tool/chip-tool.gni")

if (config_use_interactive_mode) {
  import("//build_overrides/editline.gni")
}

assert(chip_build_tools)

action("build-darwin-framwork") {
  script = "${chip_root}/scripts/build/build_darwin_framework.py"

  inputs = [ "${chip_root}/src/darwin/Framework/Matter.xcodeproj" ]

  args = [
    "--project_path",
    rebase_path("${chip_root}/src/darwin/Framework/Matter.xcodeproj",
                root_build_dir),
    "--out_path",
    "macos_framework_output",
    "--target",
    "Matter",
    "--log_path",
    rebase_path("${root_build_dir}/darwin_framework_build.log", root_build_dir),
  ]

  output_name = "Matter.framework"
  outputs = [ "${root_out_dir}/macos_framework_output/${output_name}" ]
}

config("config") {
  include_dirs = [
    ".",
    "${chip_root}/examples/darwin-framework-tool/commands/common",
    "${chip_root}/zzz_generated/darwin-framework-tool",
    "${chip_root}/zzz_generated/controller-clusters",
    "${chip_root}/examples/chip-tool/commands/clusters/ComplexArgument.h",
    "${root_out_dir}/macos_framework_output",
  ]

  framework_dirs = [ "${root_out_dir}/macos_framework_output" ]

  defines = [
    "CONFIG_ENABLE_YAML_TESTS=${config_enable_yaml_tests}",
    "CONFIG_USE_INTERACTIVE_MODE=${config_use_interactive_mode}",
  ]

  cflags = [
    "-Wconversion",
    "-fobjc-arc",
  ]
}

executable("darwin-framework-tool") {
  sources = [
    # We have to include privilege-storage.cpp here, not in the "Darwin
    # framework" library, because otherwise the weak symbols from
    # RequiredPrivilege.cpp mean we never actually pull in the function from
    # privilege-storage.cpp.
    "${chip_root}/src/app/util/privilege-storage.cpp",
    "${chip_root}/zzz_generated/chip-tool/zap-generated/cluster/ComplexArgumentParser.cpp",
    "${chip_root}/zzz_generated/controller-clusters/zap-generated/CHIPClusters.h",
    "commands/clusters/ClusterCommandBridge.h",
    "commands/clusters/ModelCommandBridge.mm",
    "commands/clusters/ReportCommandBridge.h",
    "commands/clusters/WriteAttributeCommandBridge.h",
    "commands/common/CHIPCommandBridge.mm",
    "commands/common/CHIPCommandStorageDelegate.mm",
    "commands/common/CHIPToolKeypair.mm",
    "commands/common/MTRError.mm",
    "commands/common/MTRError_Utils.h",
    "commands/common/MTRLogging.h",
    "commands/pairing/Commands.h",
    "commands/pairing/OpenCommissioningWindowCommand.h",
    "commands/pairing/OpenCommissioningWindowCommand.mm",
    "commands/pairing/PairingCommandBridge.mm",
    "commands/pairing/PairingDelegateBridge.mm",
    "commands/payload/SetupPayloadParseCommand.mm",
    "commands/storage/Commands.h",
    "commands/storage/StorageManagementCommand.mm",
    "main.mm",
  ]

  deps = [
    ":build-darwin-framwork",
    "${chip_root}/examples/chip-tool:chip-tool-utils",
    "${chip_root}/src/app/server",
    "${chip_root}/src/lib",
    "${chip_root}/src/platform",
    "${chip_root}/third_party/inipp",
    "${chip_root}/third_party/jsoncpp",
  ]

  if (config_use_interactive_mode) {
    sources += [ "commands/interactive/InteractiveCommands.mm" ]
    deps += [ "${editline_root}:editline" ]
  }

  ldflags = [
    "-rpath",
    "@executable_path/macos_framework_output/",
  ]

  frameworks = [
    "Matter.framework",
    "Security.framework",
  ]

  public_configs = [ ":config" ]

  output_dir = root_out_dir
}
