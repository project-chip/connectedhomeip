# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/chip.gni")
import("//build_overrides/pigweed.gni")

import("${chip_root}/gn/chip/tools.gni")
import("${dir_pw_unit_test}/test.gni")

config("controller_wno_deprecate") {
  cflags = [ "-Wno-deprecated-declarations" ]
}

config("includes") {
  include_dirs = [ "${chip_root}/src/controller/CHIPDeviceController.h" ]
}

shared_library("ChipDeviceCtrl") {
  output_name = "_ChipDeviceCtrl"
  output_dir = "$root_out_dir/controller/python/chip"
  include_dirs = [
    ".",
    "${chip_root}/src/controller",
  ]

  sources = [
    "ChipDeviceController-BleApplicationDelegate.cpp",
    "ChipDeviceController-BleApplicationDelegate.h",
    "ChipDeviceController-BlePlatformDelegate.cpp",
    "ChipDeviceController-BlePlatformDelegate.h",
    "ChipDeviceController-ScriptBinding.cpp",
  ]

  public_deps = [
    "${chip_root}/src/controller",
    "${chip_root}/src/lib",
    "${chip_root}/src/lib/core",
    "${chip_root}/src/lib/support",
    "${chip_root}/src/platform",
    "${chip_root}/src/transport",
  ]
  allow_circular_includes_from = [ "${chip_root}/src/controller" ]
  configs += [ ":controller_wno_deprecate" ]
}

copy("deviceControllerPyCopy1") {
  sources = [
    "chip/ChipBleBase.py",
    "chip/ChipBleUtility.py",
    "chip/ChipDeviceCtrl.py",
    "chip/ChipStack.py",
    "chip/ChipTLV.py",
    "chip/ChipUtility.py",
    "chip/__init__.py",
  ]

  if (current_os == "linux") {
    sources += [ "chip/ChipBluezMgr.py" ]
  }

  if (current_os == "mac") {
    sources += [ "chip/ChipCoreBluetoothMgr.py" ]
  }

  public_deps = [ ":ChipDeviceCtrl" ]

  outputs = [ "$root_out_dir/controller/python/chip/{{source_file_part}}" ]
}

copy("deviceControllerPyCopy2") {
  sources = [
    "build-chip-wheel.py",
    "chip-device-ctrl.py",
  ]

  public_deps = [ ":ChipDeviceCtrl" ]

  outputs = [ "$root_out_dir/controller/python/{{source_name_part}}" ]
}

pw_python_script("python") {
  script = "$root_out_dir/controller/python/build-chip-wheel"

  inputs = [
    "chip/ChipBleBase.py",
    "chip/ChipBleUtility.py",
    "chip/ChipBluezMgr.py",
    "chip/ChipDeviceCtrl.py",
    "chip/ChipStack.py",
    "chip/ChipTLV.py",
    "chip/ChipUtility.py",
    "chip/__init__.py",
  ]

  args = [
    "--package_name",
    "chip",
    "--build_number",
    "0.0",
  ]

  if (current_os == "linux") {
    inputs += [ "chip/ChipBluezMgr.py" ]
  }

  if (current_os == "mac") {
    inputs += [ "chip/ChipCoreBluetoothMgr.py" ]
  }

  public_deps = [
    ":deviceControllerPyCopy1",
    ":deviceControllerPyCopy2",
  ]

  pythontags = exec_script("//chip/python_gen_tags.py", [], "list lines", [])
  pythontagsStr = string_join("", pythontags)
  output_name = "chip-0.0-$pythontagsStr.whl"
  outputs = [ "$root_out_dir/controller/python/$output_name" ]
}
