#
#Copyright (c) 2023 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/build.gni")
import("//build_overrides/chip.gni")

config("tracing") {
  include_dirs = [ "include" ]
}

declare_args() {
  matter_enable_esp_insights_system_stats = false
}

static_library("backend") {
  output_name = "libEsp32TracingBackend"
  output_dir = "${root_out_dir}/lib"

  sources = [
    "counter.cpp",
    "counter.h",
    "esp32_tracing.cpp",
    "esp32_tracing.h",
  ]
  if (matter_enable_esp_insights_system_stats) {
    sources += [
      "insights_sys_stats.cpp",
      "insights_sys_stats.h",
    ]
    deps = [
      "${chip_root}/src/lib/core",
      "${chip_root}/src/platform",
      "${chip_root}/src/system",
    ]
  }
  public_deps = [
    "${chip_root}/src/lib/core",
    "${chip_root}/src/tracing",
  ]
}

source_set("esp32_trace_tracing") {
  public = [ "include/matter/tracing/macros_impl.h" ]
  public_configs = [ ":tracing" ]
  deps = [ ":backend" ]
}
