# Copyright (c) 2025 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/build.gni")
import("//build_overrides/chip.gni")
import("//build_overrides/pigweed.gni")
import("$dir_pw_build/python_dist.gni")

idt_files = [
  "${chip_root}/src/python_testing/post_certification_tests/production_device_checks.py",
  "${chip_root}/src/python_testing/TC_DA_1_2.py",
  "${chip_root}/src/python_testing/TC_DA_1_7.py",
]

credentials_files = [ "${chip_root}/credentials/fetch_paa_certs_from_dcl.py" ]

_version = "1.4.2"

pw_python_package("post-certification-tests-module") {
  generate_setup = {
    metadata = {
      name = "post-certification-tests-module"
      version = _version
    }
  }

  # NOTE: including the `__init__.py` file is required in order
  # setuptools could recognize it as a Python package, this is
  # needed for including extra files into the Python wheel.
  sources = [ "chip/__init__.py" ]
}

pw_python_distribution("matter-post-certification-tests") {
  packages = [ ":post-certification-tests-module" ]

  generate_setup_cfg = {
    name = "matter-post-certification-tests"
    version = _version
    include_default_pyproject_file = true
    include_extra_files_in_package_data = true
  }

  extra_files = []

  foreach(file, idt_files) {
    extra_files +=
        [ "${file} > chip/testing/idt/" + get_path_info(file, "file") ]
  }
  foreach(file, credentials_files) {
    extra_files += [ "${file} > credentials/" + get_path_info(file, "file") ]
  }
}
