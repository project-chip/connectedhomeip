# Copyright (c) 2024 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/build.gni")
import("//build_overrides/chip.gni")
import("//build_overrides/pigweed.gni")
import("//src/python_testing/matter_testing_infrastructure/data_model_xmls.gni")
import("$dir_pw_build/python.gni")
import("$dir_pw_build/python_dist.gni")
import("$dir_pw_build/zip.gni")

pw_python_package("chip-testing-module") {
  generate_setup = {
    metadata = {
      name = "chip-testing-module"
      version = "1.4.0"
      author = "Project CHIP Authors"
      license_expression = "Apache-2.0"  # Consistent license format
      url = "https://github.com/project-chip/connectedhomeip"
    }
  }

  inputs = [ "env_test.yaml" ]

  sources = [
    "chip/testing/__init__.py",
    "chip/testing/apps.py",
    "chip/testing/basic_composition.py",
    "chip/testing/choice_conformance.py",
    "chip/testing/conformance.py",
    "chip/testing/conversions.py",
    "chip/testing/global_attribute_ids.py",
    "chip/testing/matchers.py",
    "chip/testing/matter_asserts.py",
    "chip/testing/matter_testing.py",
    "chip/testing/metadata.py",
    "chip/testing/pics.py",
    "chip/testing/runner.py",
    "chip/testing/spec_parsing.py",
    "chip/testing/taglist_and_topology_test.py",
    "chip/testing/tasks.py",
    "chip/testing/timeoperations.py",
  ]
  tests = [
    "chip/testing/test_metadata.py",
    "chip/testing/test_tasks.py",
    "chip/testing/test_matter_asserts.py",
  ]
}

pw_zip("data_model_zip_1_3") {
  inputs = []
  foreach(file, data_model_XMLS_1_3) {
    zip_path = rebase_path(file, "${chip_root}/data_model/1.3/", "/")
    inputs += [ "${file} > /${zip_path}" ]
  }
  output = "${root_out_dir}/data_model/zip_1_3.zip"
}

pw_zip("data_model_zip_1_4") {
  inputs = []
  foreach(file, data_model_XMLS_1_4) {
    zip_path = rebase_path(file, "${chip_root}/data_model/1.4/", "/")
    inputs += [ "${file} > /${zip_path}" ]
  }
  output = "${root_out_dir}/data_model/zip_1_4.zip"
}

pw_zip("data_model_zip_1_4_1") {
  inputs = []
  foreach(file, data_model_XMLS_1_4_1) {
    zip_path = rebase_path(file, "${chip_root}/data_model/1.4.1/", "/")
    inputs += [ "${file} > /${zip_path}" ]
  }
  output = "${root_out_dir}/data_model/zip_1_4_1.zip"
}

pw_zip("data_model_zip_master") {
  inputs = []
  foreach(file, data_model_XMLS_master) {
    zip_path = rebase_path(file, "${chip_root}/data_model/master/", "/")
    inputs += [ "${file} > /${zip_path}" ]
  }
  output = "${root_out_dir}/data_model/zip_master.zip"
}

pw_python_distribution("chip-testing") {
  packages = [ ":chip-testing-module" ]

  generate_setup_cfg = {
    name = "chip-testing"
    version = "1.4.1"
    include_default_pyproject_file = true
    include_extra_files_in_package_data = true
  }

  public_deps = [
    ":data_model_zip_1_3",
    ":data_model_zip_1_4",
    ":data_model_zip_1_4_1",
    ":data_model_zip_master",
  ]

  extra_files = [
    "${root_out_dir}/data_model/zip_1_3.zip > chip/testing/data_model/1.3/allfiles.zip",
    "${root_out_dir}/data_model/zip_1_4.zip > chip/testing/data_model/1.4/allfiles.zip",
    "${root_out_dir}/data_model/zip_1_4_1.zip > chip/testing/data_model/1.4.1/allfiles.zip",
    "${root_out_dir}/data_model/zip_master.zip > chip/testing/data_model/master/allfiles.zip",
  ]
}
