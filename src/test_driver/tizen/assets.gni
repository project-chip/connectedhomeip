# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/build.gni")
import("//build_overrides/chip.gni")

import("${build_root}/config/compiler/compiler.gni")
import("${chip_root}/src/ble/ble.gni")

# Declare arguments used in the build_example.py script (but not declared by
# any of the imported files above), so we can reuse them for generaing default
# values for the tests assets.
declare_args() {
  chip_enable_wifi = true
  is_ubsan = false
}

# Create variant suffix string, as used by the build_examples.py script, based
# on the build configuration.
declare_args() {
  variant = ""
  if (!chip_config_network_layer_ble) {
    variant += "-no-ble"
  }
  if (!chip_enable_wifi) {
    variant += "-no-wifi"
  }
  if (is_asan) {
    variant += "-asan"
  }
  if (is_ubsan) {
    variant += "-ubsan"
  }
}

# Define default values for the test assets, which are used by the tests. These
# values mirrors the ones used by the build_examples.py script.
declare_args() {
  tizen_chip_tool =
      rebase_path("${chip_root}/out/tizen-arm-chip-tool${variant}/chip-tool")
  tizen_lighting_app_tpk = rebase_path(
          "${chip_root}/out/tizen-arm-light${variant}/package/out/org.tizen.matter.example.lighting-1.0.0.tpk")
}
