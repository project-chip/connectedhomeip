# SPDX-FileCopyrightText: 2020 Project CHIP Authors
#
# SPDX-License-Identifier: Apache-2.0

import("//build_overrides/chip.gni")
import("//build_overrides/nlassert.gni")

action("gen_asn1oid") {
  script = "gen_asn1oid.py"
  asn1oid_file = "${root_gen_dir}/include/asn1/ASN1OID.h"
  outputs = [ asn1oid_file ]
  args = [ "--output_file=" + rebase_path(asn1oid_file, root_build_dir) ]
}

source_set("asn1oid_header") {
  sources = get_target_outputs(":gen_asn1oid")

  deps = [ ":gen_asn1oid" ]
}

static_library("asn1") {
  output_name = "libASN1"

  sources = [
    "ASN1.h",
    "ASN1Config.h",
    "ASN1Error.cpp",
    "ASN1Error.h",
    "ASN1Macros.h",
    "ASN1OID.cpp",
    "ASN1Reader.cpp",
    "ASN1Time.cpp",
    "ASN1Writer.cpp",
  ]

  cflags = [ "-Wconversion" ]

  public_deps = [
    ":asn1oid_header",
    "${chip_root}/src/lib/core",
    "${chip_root}/src/lib/support",
    "${nlassert_root}:nlassert",
  ]

  public_configs = [
    "${chip_root}/src:includes",
    "${chip_root}/src/system:system_config",
  ]
}
