# SPDX-FileCopyrightText: 2020 Project CHIP Authors
#
# SPDX-License-Identifier: Apache-2.0

import("//build_overrides/chip.gni")
import("lib.gni")

static_library("lib") {
  public_deps = [
    "${chip_root}/src/app",
    "${chip_root}/src/ble",
    "${chip_root}/src/crypto",
    "${chip_root}/src/inet",
    "${chip_root}/src/lib/asn1",
    "${chip_root}/src/lib/core",
    "${chip_root}/src/lib/dnssd",
    "${chip_root}/src/lib/support",
    "${chip_root}/src/messaging",
    "${chip_root}/src/platform",
    "${chip_root}/src/protocols",
    "${chip_root}/src/setup_payload",
    "${chip_root}/src/system",
    "${chip_root}/src/transport",
  ]

  # See src/lib/lib.gni for declaration of this build arg.
  if (chip_build_controller) {
    public_deps += [ "${chip_root}/src/controller" ]
  }

  # Only include the shell if it is being used. The shell is
  # a debug feature mostly useful for embedded examples.
  # See src/lib/lib.gni for declaration of this build arg.
  if (chip_build_libshell) {
    public_deps += [ "${chip_root}/src/lib/shell" ]
  }

  # Only include the DefaultAttestationVerifier if needed in the build.
  # See src/lib/lib.gni for declaration of this build arg.
  if (chip_build_default_attestation_verifier) {
    public_deps +=
        [ "${chip_root}/src/credentials:default_attestation_verifier" ]
  }

  cflags = [ "-Wconversion" ]

  output_name = "libCHIP"

  output_dir = "${root_out_dir}/lib"

  complete_static_lib = true
}
