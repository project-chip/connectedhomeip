# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/chip_version.gni")
import("//build/tests.gni")

action("gen_chip_version") {
  script = "//scripts/gen_chip_version.py"

  version_file = "${target_gen_dir}/CHIPVersion.h"
  outputs = [ version_file ]
  args = [
    "--output_file=" + rebase_path(version_file, root_build_dir),
    "--chip_major=${chip_version_major}",
    "--chip_minor=${chip_version_minor}",
    "--chip_patch=${chip_version_patch}",
    "--chip_extra={chip_version_extra}",
  ]
}

source_set("chip_version_header") {
  sources = get_target_outputs(":gen_chip_version")

  deps = [ ":gen_chip_version" ]
}

config("support_config") {
  configs = [
    "//src:includes",
    "//src/lib:includes",
    "//src/system:system_config",
  ]

  include_dirs = [ target_gen_dir ]
}

static_library("support") {
  sources = [
    "Base64.cpp",
    "Base64.h",
    "CHIPArgParser.cpp",
    "CHIPCounter.cpp",
    "CHIPCounter.h",
    "CodeUtils.h",
    "DLLUtil.h",
    "ErrorStr.cpp",
    "ErrorStr.h",
    "FibonacciUtils.cpp",
    "FibonacciUtils.h",
    "PersistedCounter.cpp",
    "PersistedCounter.h",
    "RandUtils.cpp",
    "RandUtils.h",
    "TestUtils.cpp",
    "TestUtils.h",
    "TimeUtils.cpp",
    "TimeUtils.h",
    "logging/CHIPLogging.cpp",
    "logging/CHIPLogging.h",
    "logging/CHIPLoggingLogV.cpp",
    "verhoeff/Verhoeff.cpp",
    "verhoeff/Verhoeff.h",
    "verhoeff/Verhoeff10.cpp",
    "verhoeff/Verhoeff16.cpp",
    "verhoeff/Verhoeff32.cpp",
    "verhoeff/Verhoeff36.cpp",
  ]

  public_deps = [
    ":chip_version_header",
    "//src/lib/core:chip_config_header",
    "//third_party/nlassert",
  ]

  public_configs = [ ":support_config" ]

  if (chip_with_nlfaultinjection) {
    sources += [
      "CHIPFaultInjection.cpp",
      "CHIPFaultInjection.h",
    ]
    public_deps += [ "//third_party/nlfaultinjection" ]
  }
}
