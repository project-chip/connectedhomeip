# Copyright (c) 2023 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/build.gni")
import("//build_overrides/chip.gni")

import("${chip_root}/build/chip/chip_codegen.gni")

source_set("flat-tree") {
  sources = [
    "FlatTree.h",
    "FlatTreePosition.h",
  ]

  public_deps = [ "${chip_root}/src/lib/core" ]

  public_configs = [ "${chip_root}/src:includes" ]
}

source_set("tlv-metadata-headers") {
  sources = [
    "tlv_meta.h",  # TODO: move in separate source set
  ]
}

chip_codegen("protocol-tlv-metadata") {
  input = "protocol_messages.matter"
  generator = "cpp-tlvmeta"

  options = [ "table_name:protocols_meta" ]

  outputs = [
    "tlv/meta/protocols_meta.cpp",
    "tlv/meta/protocols_meta.h",
  ]

  deps = [
    ":flat-tree",
    ":tlv-metadata-headers",
  ]

  public_configs = [ "${chip_root}/src:includes" ]
}

source_set("protocol-decoder") {
  sources = [
    "protocol_decoder.cpp",
    "protocol_decoder.h",
  ]

  public_deps = [
    ":flat-tree",
    ":tlv-metadata-headers",
    "${chip_root}/src/lib/core",
    "${chip_root}/src/lib/support",
    "${chip_root}/src/protocols:type_definitions",
  ]

  public_configs = [ "${chip_root}/src:includes" ]
}
