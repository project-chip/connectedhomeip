apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.google.chip.chiptool"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // NOTE: This build assumes CHIP was configured and built for armeabi-v7a. Deal with
        // other archs later when build is sane.
        //  ndk {abiFilters 'armeabi-v7a'}
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    configurations.all {
        resolutionStrategy.eachDependency {DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == "androidx") {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion "${targetSdk}.+"
                }
            }
        }
    }

    if (rootProject.ext.matterBuildSrcDir == null) {
        sourceSets {
            main {
                jniLibs.srcDirs = ['libs/jniLibs']
            }
        }
    }
    else {
        sourceSets {
            main {
                java.srcDirs = [
                        'src/main/java/',
                        '../../../setup_payload/java/src',
                        '../../../controller/java/gen',
                        '../../../controller/java/src',
                ]
            }
        }

        externalNativeBuild {
            cmake {
                path file(rootProject.ext.matterBuildSrcDir + '/CMakeLists.txt')
                version '3.10.2'
            }
        }
    }
}

dependencies {
    def room_version = "2.2.5"

    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    if(rootProject.ext.matterBuildSrcDir == null) {
         // local in-source-tree copy of the dependencies. Useful for IDE compilation
        implementation fileTree(dir: "libs", include: ["*.jar", "*.so"])

        // build time dependencies
        if (project.hasProperty("chipSdkJarDir")) {
           println "Compiling using custom sdk jar file directory: ${chipSdkJarDir}"
           implementation fileTree(dir: "${chipSdkJarDir}", include: ["*.jar", "*.so"])
    }

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation "com.google.android.gms:play-services-vision:20.1.0"
    implementation 'androidx.fragment:fragment:1.3.0-beta01'
    implementation "androidx.annotation:annotation:1.1.0"
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation "androidx.core:core-ktx:1.3.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.work:work-runtime:2.3.3"
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.jjoe64:graphview:4.2.2'
}
repositories {
    mavenCentral()
}
