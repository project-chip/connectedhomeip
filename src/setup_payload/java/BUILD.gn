# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/build.gni")
import("//build_overrides/chip.gni")
import("${chip_root}/build/chip/java/config.gni")
import("${chip_root}/build/chip/java/rules.gni")

if (!build_java_matter_controller) {
  import("${build_root}/config/android_abi.gni")
}

shared_library("jni") {
  output_name = "libSetupPayloadParser"
  if (build_java_matter_controller) {
    include_dirs = java_matter_controller_dependent_paths
    output_dir = "${root_out_dir}/lib/jni"
  } else {
    output_dir = "${root_out_dir}/lib/jni/${android_abi}"
  }

  sources = [ "SetupPayloadParser-JNI.cpp" ]

  deps = [
    "${chip_root}/src/lib",
    "${chip_root}/src/setup_payload",
  ]
}

android_library("java") {
  output_name = "SetupPayloadParser.jar"

  data_deps = [ ":jni" ]

  if (!build_java_matter_controller) {
    data_deps += [ "${chip_root}/build/chip/java:shared_cpplib" ]
  }

  sources = [
    "src/chip/setuppayload/DiscoveryCapability.java",
    "src/chip/setuppayload/OptionalQRCodeInfo.java",
    "src/chip/setuppayload/SetupPayload.java",
    "src/chip/setuppayload/SetupPayloadParser.java",
  ]

  javac_flags = [
    "-Xlint:deprecation",
    "-parameters",  # Store infomation about method parameters
  ]

  # TODO: add classpath support (we likely need to add something like
  #  ..../platforms/android-21/android.jar to access BLE items)
}
