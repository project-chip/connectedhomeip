# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/chip.gni")
import("${chip_root}/src/app/common_flags.gni")

# Move this inside the GroupDataProvider once implemented Issue #11075
declare_args() {
  #Enable Multicast listenening on HardCoded IPV6 address for Test suite
  chip_enable_group_messaging_tests = false
}

config("server_config") {
  defines = []

  if (chip_app_use_echo) {
    defines += [ "CHIP_APP_USE_ECHO" ]
  }

  if (chip_enable_group_messaging_tests) {
    defines += [ "CHIP_ENABLE_GROUP_MESSAGING_TESTS" ]
  }
}

static_library("server") {
  output_name = "libCHIPAppServer"

  sources = [
    "CommissioningWindowManager.cpp",
    "CommissioningWindowManager.h",
    "Dnssd.cpp",
    "Dnssd.h",
    "EchoHandler.cpp",
    "EchoHandler.h",
    "OnboardingCodesUtil.cpp",
    "OnboardingCodesUtil.h",
    "Server.cpp",
    "Server.h",
  ]

  public_configs = [ ":server_config" ]

  cflags = [ "-Wconversion" ]

  public_deps = [
    "${chip_root}/src/app",
    "${chip_root}/src/lib/dnssd",
    "${chip_root}/src/messaging",
    "${chip_root}/src/platform",
    "${chip_root}/src/protocols",
    "${chip_root}/src/setup_payload",
    "${chip_root}/src/transport",
  ]
}
