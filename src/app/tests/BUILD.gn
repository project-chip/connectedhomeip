# Copyright (c) 2020-2024 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/build.gni")
import("//build_overrides/chip.gni")
import("//build_overrides/pigweed.gni")

import("${chip_root}/build/chip/chip_test_suite.gni")
import("${chip_root}/src/app/icd/icd.gni")
import("${chip_root}/src/crypto/crypto.gni")
import("${chip_root}/src/platform/device.gni")

static_library("helpers") {
  output_name = "libAppTestHelpers"
  output_dir = "${root_out_dir}/lib"

  sources = [
    "${chip_root}/src/app/reporting/tests/MockReportScheduler.cpp",
    "AppTestContext.cpp",
    "AppTestContext.h",
  ]

  cflags = [ "-Wconversion" ]

  deps = [
    "${chip_root}/src/access",
    "${chip_root}/src/app",
    "${chip_root}/src/lib/support",
    "${chip_root}/src/transport/raw/tests:helpers",
  ]

  public_deps = [
    "${chip_root}/src/lib/support/tests:pw-test-macros",
    "${chip_root}/src/messaging/tests:helpers",
  ]
}

source_set("ota-requestor-test-srcs") {
  sources = [
    "${chip_root}/src/app/clusters/ota-requestor/DefaultOTARequestorStorage.cpp",
    "${chip_root}/src/app/clusters/ota-requestor/DefaultOTARequestorStorage.h",
    "${chip_root}/src/app/clusters/ota-requestor/OTARequestorStorage.h",
  ]

  public_deps = [
    "${chip_root}/src/app/common:cluster-objects",
    "${chip_root}/src/lib/core",
  ]
}

source_set("thread-network-directory-test-srcs") {
  sources = [
    "${chip_root}/src/app/clusters/thread-network-directory-server/DefaultThreadNetworkDirectoryStorage.cpp",
    "${chip_root}/src/app/clusters/thread-network-directory-server/DefaultThreadNetworkDirectoryStorage.h",
    "${chip_root}/src/app/clusters/thread-network-directory-server/ThreadNetworkDirectoryStorage.h",
  ]

  public_deps = [
    "${chip_root}/src/app/common:cluster-objects",
    "${chip_root}/src/lib/core",
  ]
}

source_set("time-sync-data-provider-test-srcs") {
  public_deps = [
    "${chip_root}/src/app/clusters/time-synchronization-server:time-sync-data-provider",
    "${chip_root}/src/app/common:cluster-objects",
    "${chip_root}/src/app/persistence:persistence",
    "${chip_root}/src/lib/core",
  ]
}

source_set("power-cluster-test-srcs") {
  sources = [
    "${chip_root}/src/app/clusters/power-source-server/power-source-server.cpp",
  ]

  public_deps = [
    "${chip_root}/src/app/common:cluster-objects",
    "${chip_root}/src/app/util/mock:mock_codegen_data_model",
    "${chip_root}/src/app/util/mock:mock_ember",
    "${chip_root}/src/lib/core",
  ]
}

source_set("actions-cluster-test-srcs") {
  sources =
      [ "${chip_root}/src/app/clusters/actions-server/actions-server.cpp" ]

  public_deps = [
    "${chip_root}/src/app/common:cluster-objects",
    "${chip_root}/src/app/util/mock:mock_codegen_data_model",
    "${chip_root}/src/app/util/mock:mock_ember",
    "${chip_root}/src/lib/core",
  ]
}

source_set("scenes-table-test-srcs") {
  # Tests currently validate/use capping logic from codegen, so we add codegen support
  # and mock data models.
  sources = [
    "${chip_root}/src/app/clusters/scenes-server/CodegenAttributeValuePairValidator.cpp",
    "${chip_root}/src/app/clusters/scenes-server/CodegenAttributeValuePairValidator.h",
  ]

  public_deps = [
    "${chip_root}/src/app",
    "${chip_root}/src/app/clusters/scenes-server:scenes",
    "${chip_root}/src/app/common:cluster-objects",
    "${chip_root}/src/app/storage:fabric-table",
    "${chip_root}/src/app/util/mock:mock_codegen_data_model",
    "${chip_root}/src/app/util/mock:mock_ember",
    "${chip_root}/src/lib/core",
  ]
}

source_set("operational-state-test-srcs") {
  sources = [ "${chip_root}/src/app/clusters/operational-state-server/operational-state-cluster-objects.h" ]

  public_deps = [
    "${chip_root}/src/app/common:cluster-objects",
    "${chip_root}/src/lib/core",
  ]
}

source_set("thread-border-router-management-test-srcs") {
  sources = [
    "${chip_root}/src/app/clusters/thread-border-router-management-server/thread-border-router-management-server.cpp",
    "${chip_root}/src/app/clusters/thread-border-router-management-server/thread-border-router-management-server.h",
    "${chip_root}/src/app/clusters/thread-border-router-management-server/thread-br-delegate.h",

    # Sources needed because codegen integration of general commissioning provides general commissioning instance
    "${chip_root}/src/app/clusters/general-commissioning-server/CodegenIntegration.cpp",
    "${chip_root}/src/app/clusters/general-commissioning-server/CodegenIntegration.h",
    "${chip_root}/src/app/clusters/group-key-mgmt-server/CodegenIntegration.cpp",
  ]

  public_deps = [
    "${chip_root}/src/app",
    "${chip_root}/src/app/clusters/general-commissioning-server:general-commissioning-server",
    "${chip_root}/src/app/clusters/group-key-mgmt-server",
    "${chip_root}/src/app/common:cluster-objects",
    "${chip_root}/src/app/util/mock:mock_codegen_data_model",
    "${chip_root}/src/app/util/mock:mock_ember",
    "${chip_root}/src/lib/core",
  ]
}

source_set("ecosystem-information-test-srcs") {
  sources = [
    "${chip_root}/src/app/clusters/ecosystem-information-server/ecosystem-information-server.cpp",
    "${chip_root}/src/app/clusters/ecosystem-information-server/ecosystem-information-server.h",
  ]
  public_deps = [
    "${chip_root}/src/app:interaction-model",
    "${chip_root}/src/app/common:cluster-objects",
    "${chip_root}/src/lib/core",
  ]
}

source_set("closure-dimension-test-srcs") {
  sources = [
    "${chip_root}/src/app/clusters/closure-dimension-server/closure-dimension-cluster-logic.cpp",
    "${chip_root}/src/app/clusters/closure-dimension-server/closure-dimension-cluster-logic.h",
    "${chip_root}/src/app/clusters/closure-dimension-server/closure-dimension-cluster-objects.h",
    "${chip_root}/src/app/clusters/closure-dimension-server/closure-dimension-delegate.h",
    "${chip_root}/src/app/clusters/closure-dimension-server/closure-dimension-matter-context.cpp",
    "${chip_root}/src/app/clusters/closure-dimension-server/closure-dimension-matter-context.h",
    "${chip_root}/src/app/clusters/closure-dimension-server/closure-dimension-server.cpp",
    "${chip_root}/src/app/clusters/closure-dimension-server/closure-dimension-server.h",
  ]
  public_deps = [
    "${chip_root}/src/app:interaction-model",
    "${chip_root}/src/app/cluster-building-blocks:cluster-building-blocks",
    "${chip_root}/src/app/common:cluster-objects",
    "${chip_root}/src/lib/core",
    "${chip_root}/src/lib/support",
    "${chip_root}/src/platform",
    "${chip_root}/zzz_generated/app-common/clusters/ClosureDimension:metadata",
  ]
}

source_set("app-test-stubs") {
  sources = [
    "test-ember-api.cpp",
    "test-ember-api.h",

    # The overrides in these files are overrides from ember generated code
    # and the data model interface is NOT aware of such functionality
    #
    # TODO: ideally tests should have been written via mock ember, however mock ember did
    #       not exist at that time. We should completely re-write how these tests access
    #       the data via the DataModel interface
    "test-interaction-model-api.cpp",
    "test-interaction-model-api.h",
  ]

  public_deps = [
    "${chip_root}/src/app/util/mock:mock_codegen_data_model",
    "${chip_root}/src/app/util/mock:mock_ember",
    "${chip_root}/src/lib/core",
    "${chip_root}/src/lib/support",
  ]
}

source_set("closure-control-test-srcs") {
  sources = [
    "${chip_root}/src/app/clusters/closure-control-server/closure-control-cluster-delegate.h",
    "${chip_root}/src/app/clusters/closure-control-server/closure-control-cluster-logic.cpp",
    "${chip_root}/src/app/clusters/closure-control-server/closure-control-cluster-logic.h",
    "${chip_root}/src/app/clusters/closure-control-server/closure-control-cluster-matter-context.h",
    "${chip_root}/src/app/clusters/closure-control-server/closure-control-cluster-objects.h",
  ]

  deps = [
    "${chip_root}/src/app",
    "${chip_root}/src/app:interaction-model",
    "${chip_root}/src/app/cluster-building-blocks:cluster-building-blocks",
    "${chip_root}/src/app/common:enums",
    "${chip_root}/src/lib/core",
    "${chip_root}/src/lib/support",
    "${chip_root}/zzz_generated/app-common/clusters/ClosureControl:metadata",
  ]
}

chip_test_suite("tests") {
  output_name = "libAppTests"

  test_sources = [
    "TestAclAttribute.cpp",
    "TestAclCommand.cpp",
    "TestAclEvent.cpp",
    "TestActionsCluster.cpp",
    "TestAttributeAccessInterfaceCache.cpp",
    "TestAttributePathExpandIterator.cpp",
    "TestAttributePathParams.cpp",
    "TestAttributeValueDecoder.cpp",
    "TestAttributeValueEncoder.cpp",
    "TestBasicCommandPathRegistry.cpp",
    "TestBuilderParser.cpp",
    "TestCheckInHandler.cpp",
    "TestClosureControlClusterLogic.cpp",
    "TestClosureControlClusterObjects.cpp",
    "TestClosureControlConformance.cpp",
    "TestClosureDimensionCluster.cpp",
    "TestClosureDimensionClusterObjects.cpp",
    "TestCommandHandlerInterfaceRegistry.cpp",
    "TestCommandInteraction.cpp",
    "TestCommandPathParams.cpp",
    "TestConcreteAttributePath.cpp",
    "TestDataModelSerialization.cpp",
    "TestDefaultOTARequestorStorage.cpp",
    "TestDefaultSafeAttributePersistenceProvider.cpp",
    "TestDefaultTermsAndConditionsProvider.cpp",
    "TestDefaultThreadNetworkDirectoryStorage.cpp",
    "TestEcosystemInformationCluster.cpp",
    "TestEventLoggingNoUTCTime.cpp",
    "TestEventOverflow.cpp",
    "TestEventPathParams.cpp",
    "TestFabricScopedEventLogging.cpp",
    "TestInteractionModelEngine.cpp",
    "TestMessageDef.cpp",
    "TestNumericAttributeTraits.cpp",
    "TestOperationalStateClusterObjects.cpp",
    "TestPendingResponseTrackerImpl.cpp",
    "TestPowerSourceCluster.cpp",
    "TestReadInteraction.cpp",
    "TestReportScheduler.cpp",
    "TestReportingEngine.cpp",
    "TestServer.cpp",
    "TestStatusIB.cpp",
    "TestStatusResponseMessage.cpp",
    "TestTestEventTriggerDelegate.cpp",
    "TestTimeSyncDataProvider.cpp",
    "TestTimedHandler.cpp",
    "TestWriteInteraction.cpp",
  ]

  cflags = [ "-Wconversion" ]

  public_deps = [
    ":app-test-stubs",
    ":closure-control-test-srcs",
    ":closure-dimension-test-srcs",
    ":ecosystem-information-test-srcs",
    ":operational-state-test-srcs",
    ":ota-requestor-test-srcs",
    ":thread-network-directory-test-srcs",
    ":time-sync-data-provider-test-srcs",
    "${chip_root}/src/app",
    "${chip_root}/src/app:attribute-persistence",
    "${chip_root}/src/app/common:cluster-objects",
    "${chip_root}/src/app/data-model-provider/tests:encode-decode",
    "${chip_root}/src/app/icd/client:handler",
    "${chip_root}/src/app/icd/client:manager",
    "${chip_root}/src/app/icd/server:configuration-data",
    "${chip_root}/src/app/server",
    "${chip_root}/src/app/server:terms_and_conditions",
    "${chip_root}/src/app/tests:helpers",
    "${chip_root}/src/app/util/mock:mock_codegen_data_model",
    "${chip_root}/src/app/util/mock:mock_ember",
    "${chip_root}/src/data-model-providers/codegen:instance-header",
    "${chip_root}/src/lib/core",
    "${chip_root}/src/lib/core:string-builder-adapters",
    "${chip_root}/src/lib/support:test_utils",
    "${chip_root}/src/lib/support:testing",
    "${chip_root}/src/lib/support/tests:pw-test-macros",
  ]

  if (chip_device_platform != "android") {
    test_sources += [
      "TestCertificateTableImpl.cpp",
      "TestExtensionFieldSets.cpp",
      "TestSceneTable.cpp",
    ]
    public_deps += [
      ":actions-cluster-test-srcs",
      ":power-cluster-test-srcs",
      ":scenes-table-test-srcs",
      "${chip_root}/src/app/clusters/tls-certificate-management-server:certificate-table",
    ]
  }

  # TestCommissioningWindowManager uses MockClock to advance time and force timer expiration which
  # does not behave as expected on darwin
  if (chip_config_network_layer_ble && chip_device_platform == "linux") {
    test_sources += [ "TestCommissioningWindowManager.cpp" ]
    public_deps += [
      "${chip_root}/src/app/clusters/administrator-commissioning-server:administrator-commissioning-server",
      "${chip_root}/src/app/server",
      "${chip_root}/src/messaging/tests/echo:common",
    ]
  }
  if (!chip_fake_platform) {
    test_sources += [ "TestThreadBorderRouterManagementCluster.cpp" ]
    public_deps += [ ":thread-border-router-management-test-srcs" ]
  }

  if (!chip_fake_platform) {
    test_sources += [ "TestFailSafeContext.cpp" ]
  }

  # DefaultICDClientStorage assumes that raw AES key is used by the application
  if (chip_crypto != "psa") {
    test_sources += [ "TestDefaultICDClientStorage.cpp" ]
  }

  if (chip_persist_subscriptions) {
    test_sources += [ "TestSimpleSubscriptionResumptionStorage.cpp" ]
  }

  # On NRF platforms, the allocation of a large number of pbufs in this test
  # to exercise chunking causes it to run out of memory. For now, disable it there.
  #
  if (chip_device_platform != "nrfconnect") {
    test_sources += [ "TestBufferedReadCallback.cpp" ]
    test_sources += [ "TestClusterStateCache.cpp" ]
  }

  # On NRF, Open IoT SDK and fake platforms we do not have a realtime clock available,
  # so TestEventLogging.cpp would be testing the same thing as TestEventLoggingNoUTCTime,
  # but it's not set up to deal with the timestamps being that low.
  if (chip_device_platform != "nrfconnect" && chip_device_platform != "fake") {
    test_sources += [ "TestEventLogging.cpp" ]
  }
}
