# Copyright (c) 2021 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/chip.gni")

import("${chip_root}/src/platform/device.gni")

assert(chip_device_platform == "qpg")

if (chip_enable_openthread) {
  import("//build_overrides/openthread.gni")
}

static_library("qpg") {
  sources = [
    "../FreeRTOS/SystemTimeSupport.cpp",
    "../SingletonConfigurationManager.cpp",
    "BLEManagerImpl.cpp",
    "BLEManagerImpl.h",
    "BlePlatformConfig.h",
    "CHIPDevicePlatformConfig.h",
    "CHIPDevicePlatformEvent.h",
    "CHIPPlatformConfig.h",
    "ConfigurationManagerImpl.cpp",
    "ConfigurationManagerImpl.h",
    "ConnectivityManagerImpl.cpp",
    "ConnectivityManagerImpl.h",
    "DiagnosticDataProviderImpl.cpp",
    "DiagnosticDataProviderImpl.h",
    "FactoryDataProvider.cpp",
    "FactoryDataProvider.h",
    "InetPlatformConfig.h",
    "OTAImageProcessorImpl.cpp",
    "PlatformManagerImpl.cpp",
    "PlatformManagerImpl.h",
    "SystemPlatformConfig.h",
    "qpgConfig.cpp",
    "qpgConfig.h",
  ]

  deps = [
    "${chip_root}/src/app:app_config",
    "${chip_root}/src/platform/logging:headers",
  ]

  public = [ "${chip_root}/src/credentials/DeviceAttestationCredsProvider.h" ]

  public_deps = [ "${chip_root}/src/platform:platform_base" ]

  public_configs = []

  sources += [
    "KeyValueStoreManagerImpl.cpp",
    "KeyValueStoreManagerImpl.h",
  ]

  if (chip_enable_openthread) {
    if (chip_openthread_ftd) {
      public_deps += [ "${openthread_root}:libopenthread-ftd" ]
    } else {
      public_deps += [ "${openthread_root}:libopenthread-mtd" ]
    }

    public_configs += [
      "${chip_root}/third_party/openthread/platforms/qpg:openthread_qpg_config",
    ]

    sources += [
      "../OpenThread/OpenThreadUtils.cpp",
      "ThreadStackManagerImpl.cpp",
      "ThreadStackManagerImpl.h",
    ]
    deps += [ "${chip_root}/src/app:app_config" ]

    if (chip_mdns == "platform") {
      sources += [
        "../OpenThread/DnssdImpl.cpp",
        "../OpenThread/OpenThreadDnssdImpl.cpp",
        "../OpenThread/OpenThreadDnssdImpl.h",
      ]
      deps += [ "${chip_root}/src/lib/dnssd:platform_header" ]
    }
  }
}

source_set("logging") {
  deps = [
    "${chip_root}/src/platform:platform_base",
    "${chip_root}/src/platform/logging:headers",
  ]
  sources = [ "Logging.cpp" ]
}
