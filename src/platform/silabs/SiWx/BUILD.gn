# Copyright (c) 2021 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/chip.gni")
import("${chip_root}/build/chip/buildconfig_header.gni")
import("${chip_root}/src/crypto/crypto.gni")
import("${chip_root}/src/platform/device.gni")
import("${chip_root}/src/platform/silabs/multi-ota/args.gni")
import("${chip_root}/src/platform/silabs/wifi/args.gni")
import("${chip_root}/third_party/silabs/SiWx917_sdk.gni")
import("${chip_root}/third_party/silabs/silabs_board.gni")
import("${matter_support_root}/provision/args.gni")
silabs_platform_dir = "${chip_root}/src/platform/silabs"

assert(chip_device_platform == "SiWx917")
assert(sl_matter_enable_siwx_ble,
       "sl_matter_enable_siwx_ble must be enabled for SiWx917")

if (chip_crypto == "platform") {
  import("//build_overrides/mbedtls.gni")
}

source_set("logging") {
  sources = [ "${chip_root}/src/platform/silabs/Logging.cpp" ]
  deps = [
    "${chip_root}/src/platform:platform_base",
    "${chip_root}/src/platform/logging:headers",
  ]
}

static_library("SiWx") {
  sources = [
    "${silabs_platform_dir}/BLEManagerImpl.h",
    "${silabs_platform_dir}/BlePlatformConfig.h",
    "${silabs_platform_dir}/CHIPDevicePlatformConfig.h",
    "${silabs_platform_dir}/CHIPDevicePlatformEvent.h",
    "${silabs_platform_dir}/CHIPMem-Platform.cpp",
    "${silabs_platform_dir}/CHIPPlatformConfig.h",
    "${silabs_platform_dir}/ConfigurationManagerImpl.cpp",
    "${silabs_platform_dir}/ConfigurationManagerImpl.h",
    "${silabs_platform_dir}/ConnectivityManagerImpl.h",
    "${silabs_platform_dir}/ConnectivityManagerImpl_WIFI.cpp",
    "${silabs_platform_dir}/DiagnosticDataProviderImpl.cpp",
    "${silabs_platform_dir}/DiagnosticDataProviderImpl.h",
    "${silabs_platform_dir}/InetPlatformConfig.h",
    "${silabs_platform_dir}/KeyValueStoreManagerImpl.cpp",
    "${silabs_platform_dir}/KeyValueStoreManagerImpl.h",
    "${silabs_platform_dir}/MigrationManager.cpp",
    "${silabs_platform_dir}/MigrationManager.h",
    "${silabs_platform_dir}/NetworkCommissioningWiFiDriver.cpp",
    "${silabs_platform_dir}/NetworkCommissioningWiFiDriver.h",
    "${silabs_platform_dir}/PlatformManagerImpl.cpp",
    "${silabs_platform_dir}/PlatformManagerImpl.h",
    "${silabs_platform_dir}/SilabsConfig.cpp",
    "${silabs_platform_dir}/SilabsConfig.h",
    "${silabs_platform_dir}/SystemPlatformConfig.h",
    "${silabs_platform_dir}/platformAbstraction/SilabsPlatform.cpp",
    "${silabs_platform_dir}/platformAbstraction/SilabsPlatform.h",
    "${silabs_platform_dir}/platformAbstraction/SilabsPlatformBase.h",
    "${silabs_platform_dir}/platformAbstraction/WiseMcuSpam.cpp",
    "../../FreeRTOS/SystemTimeSupport.cpp",
    "../../SingletonConfigurationManager.cpp",
    "SiWxPlatformInterface.h",
    "ble/BLEManagerImpl.cpp",
    "ble/sl_si91x_ble_init.cpp",
    "ble/sl_si91x_ble_init.h",
  ]

  public_deps = [
    "${chip_root}/src/app/icd/server:icd-server-config",
    "${chip_root}/src/platform:platform_base",

    # sl_matter_enable_siwx_ble
    "${silabs_platform_dir}/SiWx/ble:siwx-ble",
    "${silabs_platform_dir}/platformAbstraction:wisemcu",
  ]
  deps = [
    "${chip_root}/src/app/common:ids",
    "${chip_root}/src/platform/logging:headers",
    "${sl_provision_root}:headers",
  ]

  if (chip_enable_multi_ota_requestor) {
    public_deps += [ "${silabs_platform_dir}/multi-ota:multi-ota-siwx" ]
  } else if (chip_enable_ota_requestor) {
    sources += [
      "${silabs_platform_dir}/OTAImageProcessorImpl.h",
      "OTAImageProcessorImpl.cpp",
    ]
  }

  # Add platform crypto implementation
  if (chip_crypto == "platform") {
    if (sl_si91x_crypto_flavor == "tinycrypt") {
      sources += [ "CHIPCryptoPALTinyCrypt.cpp" ]
    }

    if (sl_si91x_crypto_flavor == "psa") {
      sources += [ "${silabs_platform_dir}/efr32/CHIPCryptoPALPsaEfr32.cpp" ]
    }

    public_deps += [
      "${chip_root}/src/crypto",
      "${silabs_platform_dir}/wifi:wifi-platform",
    ]
  }
}
