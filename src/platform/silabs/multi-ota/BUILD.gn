# Copyright (c) 2021 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/chip.gni")
import("//build_overrides/efr32_sdk.gni")
import("${chip_root}/third_party/silabs/silabs_board.gni")
if (wifi_soc == true) {
  import("${chip_root}/third_party/silabs/SiWx917_sdk.gni")
} else {
  import("${chip_root}/third_party/silabs/efr32_sdk.gni")
}
import("${matter_support_root}/provision/args.gni")

config("multi-ota-config") {
  include_dirs = [
    ".",
    "./SiWx917",
  ]
}

source_set("multi-ota") {
  sources = [
    "OTAFactoryDataProcessor.cpp",
    "OTAHooks.cpp",
    "OTAMultiImageProcessorImpl.cpp",
    "OTATlvProcessor.cpp",
  ]

  public_configs = [ ":multi-ota-config" ]

  deps = [
    "${chip_root}/src/lib/core",
    "${chip_root}/src/lib/support",
    "${sl_provision_root}:headers",
    "../platformAbstraction:platform-abstraction",
  ]
  if (chip_enable_wifi) {
    deps += [ "${silabs_platform_dir}/wifi:wifi-platform" ]
  }

  #  OTA processor for SiWx917 family
  if ((chip_enable_wifi && use_SiWx917) ||
      (chip_enable_wifi && wifi_soc == true)) {
    sources += [ "SiWx917/OTAWiFiFirmwareProcessor.cpp" ]
  }

  # OTA processor for EFR32 host
  if (wifi_soc == false) {
    sources += [ "OTAFirmwareProcessor.cpp" ]
  }

  if (chip_enable_multi_ota_encryption) {
    sources += [
      "${silabs_platform_dir}/multi-ota/OtaTlvEncryptionKey.cpp",
      "${silabs_platform_dir}/multi-ota/OtaTlvEncryptionKey.h",
    ]
  }

  if (chip_enable_ota_custom_tlv_testing) {
    sources += [
      "${silabs_platform_dir}/multi-ota/OTACustomProcessor.cpp",
      "${silabs_platform_dir}/multi-ota/OTACustomProcessor.h",
    ]
  }
}
