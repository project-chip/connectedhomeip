# Copyright (c) 2021 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/chip.gni")
import("//build_overrides/pigweed.gni")

import("${chip_root}/src/platform/device.gni")

assert(chip_device_platform == "mt793x")

if (chip_enable_openthread) {
  import("//build_overrides/openthread.gni")
}

static_library("mt793x") {
  sources = [
    "Logging.cpp",
    "CHIPMem-Platform.cpp",
    "../SingletonConfigurationManager.cpp",
    "CHIPDevicePlatformEvent.h",
    "ConfigurationManagerImpl.cpp",
    "ConfigurationManagerImpl.h",
    "ConnectivityManagerImpl.cpp",
    "ConnectivityManagerImpl.h",
    "DeviceNetworkProvisioningDelegateImpl.cpp",
    "DeviceNetworkProvisioningDelegateImpl.h",
    "DiagnosticDataProviderImpl.cpp",
    "DiagnosticDataProviderImpl.h",
    "KeyValueStoreManagerImpl.cpp",
    "KeyValueStoreManagerImpl.h",
    "MT793XConfig.cpp",
    "MT793XConfig.h",
    "PlatformManagerImpl.cpp",
    "PlatformManagerImpl.h",
    "SystemTimeSupport.cpp",
    "SystemPlatformConfig.h",
    "DnssdImpl.cpp",
    "MtkWiFiEvents.cpp",
  ]

  if (chip_enable_ble) {
    sources += [
      "freertos_bluetooth.c",
      "freertos_bluetooth.h",
      "gatt_db.c",
      "gatt_db.h",
    ]
  }

  if (chip_enable_ota_requestor) {
    sources += [
      "OTAImageProcessorImpl.cpp",
      "OTAImageProcessorImpl.h",
    ]
  }

  public_deps = [ "${chip_root}/src/app:app_config",
                  "${chip_root}/src/platform:platform_base",
                ]

  deps = [
    "$dir_pw_kvs:crc16",
    "$dir_pw_log",
  ]

  public_deps += [
    "$dir_pw_checksum",
    "$dir_pw_kvs",
    "${chip_root}/third_party/mt793x_sdk/mDNSResponder",
  ]

  if (chip_enable_openthread) {
    if (chip_openthread_ftd) {
      public_deps +=
          [ "${chip_root}/third_party/openthread/repo:libopenthread-ftd" ]
    } else {
      public_deps +=
          [ "${chip_root}/third_party/openthread/repo:libopenthread-mtd" ]
    }

    sources += [
      "../OpenThread/OpenThreadUtils.cpp",
      "ThreadStackManagerImpl.cpp",
      "ThreadStackManagerImpl.h",
    ]

    if (chip_mdns == "platform") {
      sources += [ "../OpenThread/DnssdImpl.cpp" ]
      deps += [ "${chip_root}/src/lib/dnssd:platform_header" ]
    }
  }

  if (chip_enable_wifi) {
    sources += [
      "ConnectivityManagerImpl_WIFI.cpp",
      "ServiceProvisioning.cpp",
      "ServiceProvisioning.h",
    ]
  }
}
