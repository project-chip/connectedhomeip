# Copyright (c) 2021 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/build.gni")
import("//build_overrides/chip.gni")
import("//build_overrides/tizen.gni")

import("${build_root}/config/linux/pkg_config.gni")

import("${chip_root}/src/platform/device.gni")

if (chip_enable_openthread) {
  import("//build_overrides/openthread.gni")
  import("//build_overrides/ot_br_posix.gni")
}

assert(chip_device_platform == "tizen")

static_library("Tizen") {
  sources = [
    "../DeviceSafeQueue.cpp",
    "../DeviceSafeQueue.h",
    "../GLibTypeDeleter.h",
    "../SingletonConfigurationManager.cpp",
    "AppPreference.cpp",
    "AppPreference.h",
    "CHIPDevicePlatformConfig.h",
    "CHIPDevicePlatformEvent.h",
    "CHIPPlatformConfig.h",
    "ConfigurationManagerImpl.cpp",
    "ConfigurationManagerImpl.h",
    "ConnectivityManagerImpl.cpp",
    "ConnectivityManagerImpl.h",
    "ConnectivityUtils.cpp",
    "ConnectivityUtils.h",
    "DeviceInstanceInfoProviderImpl.cpp",
    "DeviceInstanceInfoProviderImpl.h",
    "DiagnosticDataProviderImpl.cpp",
    "DiagnosticDataProviderImpl.h",
    "ErrorUtils.cpp",
    "ErrorUtils.h",
    "InetPlatformConfig.h",
    "KeyValueStoreManagerImpl.cpp",
    "KeyValueStoreManagerImpl.h",
    "NetworkCommissioningDriver.h",
    "NetworkCommissioningEthernetDriver.cpp",
    "PlatformManagerImpl.cpp",
    "PlatformManagerImpl.h",
    "PosixConfig.cpp",
    "PosixConfig.h",
    "SystemInfo.cpp",
    "SystemInfo.h",
    "SystemPlatformConfig.h",
    "SystemTimeSupport.cpp",
  ]

  deps = [ "${chip_root}/src/setup_payload" ]

  public_deps = [
    "${chip_root}/src/app:app_config",
    "${chip_root}/src/platform:platform_base",
    "${chip_root}/src/platform/logging:headers",
    "${tizen_root}:tizen",
  ]

  public_configs = []

  if (chip_mdns == "platform") {
    sources += [
      "DnssdImpl.cpp",
      "DnssdImpl.h",
    ]

    deps += [ "${chip_root}/src/lib/dnssd:platform_header" ]
  }

  if (chip_enable_ble) {
    sources += [
      "BLEManagerImpl.cpp",
      "BLEManagerImpl.h",
      "BlePlatformConfig.h",
      "ChipDeviceScanner.cpp",
      "ChipDeviceScanner.h",
    ]
  }

  if (chip_enable_openthread) {
    sources += [
      "NetworkCommissioningThreadDriver.cpp",
      "ThreadStackManagerImpl.cpp",
      "ThreadStackManagerImpl.h",
    ]
    deps += [ "${chip_root}/src/app:app_config" ]
  }

  if (chip_enable_wifi) {
    sources += [
      "NetworkCommissioningWiFiDriver.cpp",
      "WiFiManager.cpp",
      "WiFiManager.h",
    ]
  }

  cflags = [ "-Wconversion" ]
}

source_set("logging") {
  deps = [
    "${chip_root}/src/platform:platform_base",
    "${chip_root}/src/platform/logging:headers",
    "${tizen_root}:tizen",
  ]
  sources = [ "Logging.cpp" ]
}
