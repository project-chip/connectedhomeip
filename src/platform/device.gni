# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/chip.gni")

declare_args() {
  # Device platform layer: freertos, nrf5, efr32, none.
  device_platform = "auto"
}

if (device_platform == "auto") {
  if (current_os == "mac" || current_os == "ios") {
    device_platform = "darwin"
  } else if (current_os == "linux") {
    # FIXME: enable openthread on nrf5 gn build
    device_platform = "linux"
  } else {
    device_platform = "none"
  }
}

declare_args() {
  # Enable openthread support.
  chip_enable_openthread = (device_platform == "linux")
}

device_layer = "none"
if (device_platform == "darwin") {
  device_layer = "Darwin"
} else if (device_platform == "linux") {
  device_layer = "Linux"
} else if (device_platform == "nrf5") {
  device_layer = "nRF5"
} else if (device_platform == "efr32") {
  device_layer = "EFR32"
}

ble_platform_config_include = ""
chip_device_platform_config_include = ""
chip_platform_config_include = ""
inet_platform_config_include = ""
system_platform_config_include = ""

if (device_layer != "none") {
  ble_platform_config_include =
      "<platform/" + device_layer + "/BlePlatformConfig.h>"
  chip_device_platform_config_include =
      "<platform/" + device_layer + "/CHIPDevicePlatformConfig.h>"
  chip_platform_config_include =
      "<platform/" + device_layer + "/CHIPPlatformConfig.h>"
  inet_platform_config_include =
      "<platform/" + device_layer + "/InetPlatformConfig.h>"
  system_platform_config_include =
      "<platform/" + device_layer + "/SystemPlatformConfig.h>"
}

assert((current_os != "freertos" && device_platform == "none") ||
           device_platform == "darwin" || device_platform == "nrf5" ||
           device_platform == "linux" || device_platform == "efr32",
       "Please select a valid value for device_platform")
