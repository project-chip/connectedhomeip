# Copyright (c) 2021 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/chip.gni")
import("//build_overrides/chip.gni")
import("//build_overrides/zephyr.gni")

import("${chip_root}/src/platform/Zephyr/args.gni")
import("${chip_root}/src/platform/device.gni")

assert(chip_device_platform == "zephyr")

static_library("Zephyr") {
  sources = [
    "../SingletonConfigurationManager.cpp",
    "BLEAdvertisingArbiter.cpp",
    "BLEAdvertisingArbiter.h",
    "BLEManagerImpl.cpp",
    "BLEManagerImpl.h",
    "BlePlatformConfig.h",
    "CHIPDevicePlatformConfig.h",
    "CHIPDevicePlatformEvent.h",
    "CHIPPlatformConfig.h",
    "ConfigurationManagerImpl.cpp",
    "ConfigurationManagerImpl.h",
    "ConnectivityManagerImpl.cpp",
    "ConnectivityManagerImpl.h",
    "DiagnosticDataProviderImpl.cpp",
    "DiagnosticDataProviderImpl.h",
    "DiagnosticDataProviderImplGetter.cpp",
    "InetPlatformConfig.h",
    "InetUtils.cpp",
    "KeyValueStoreManagerImpl.cpp",
    "KeyValueStoreManagerImpl.h",
    "PlatformManagerImpl.cpp",
    "PlatformManagerImpl.h",
    "SysHeapMalloc.h",
    "SystemPlatformConfig.h",
    "SystemTimeSupport.cpp",
    "ZephyrConfig.cpp",
    "ZephyrConfig.h",
  ]

  public_deps = [ "${chip_root}/src/platform:platform_base" ]
  deps = [
    "${chip_root}/src/app:app_config",
    "${chip_root}/src/platform/logging:headers",
  ]

  if (chip_enable_openthread) {
    sources += [
      "../OpenThread/OpenThreadUtils.cpp",
      "ThreadStackManagerImpl.cpp",
      "ThreadStackManagerImpl.h",
    ]
    deps += [ "${chip_root}/src/app:app_config" ]

    if (chip_mdns == "platform") {
      sources += [
        "../OpenThread/DnssdImpl.cpp",
        "../OpenThread/OpenThreadDnssdImpl.cpp",
        "../OpenThread/OpenThreadDnssdImpl.h",
      ]
      deps += [ "${chip_root}/src/lib/dnssd:platform_header" ]
    }
  }

  if (chip_enable_nfc_onboarding_payload) {
    sources += [
      "NFCOnboardingPayloadManagerImpl.cpp",
      "NFCOnboardingPayloadManagerImpl.h",
    ]
  }

  if (chip_malloc_sys_heap) {
    sources += [ "SysHeapMalloc.cpp" ]
  }

  cflags = [ "-Wconversion" ]
}

source_set("logging") {
  deps = [
    "${chip_root}/src/platform:platform_base",
    "${chip_root}/src/platform/logging:headers",
  ]
  sources = [ "Logging.cpp" ]
}
