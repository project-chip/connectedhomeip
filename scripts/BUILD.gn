# Copyright (c) 2021 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/build.gni")
import("//build_overrides/chip.gni")

import("//build_overrides/pigweed.gni")
import("$dir_pw_build/python_dist.gni")

import("${chip_root}/src/controller/flags.gni")

# This target creates a single Python package and wheel for yamltests. It will
# merge all Python dependencies Matter. The output is located in:
#   out/obj/scripts/matter_yamltests_distribution/  <- source files here
#   out/obj/scripts/matter_yamltests_distribution._build_wheel/matter_yamltests-0.0.1-py3-none-any.whl
pw_python_distribution("matter_yamltests_distribution") {
  packages = [ "${chip_root}/scripts/py_matter_yamltests:matter-yamltests" ]
  generate_setup_cfg = {
    name = "matter_yamltests"
    version = "0.0.1"
    include_default_pyproject_file = true
    append_date_to_version = true
  }
}

pw_python_distribution("matter_iot") {
  packages = [
    "${chip_root}/scripts/py_matter_yamltests:matter-yamltests",
    "${chip_root}/scripts/py_matter_idl:matter-idl",
    "${chip_root}/src/python_testing/matter_testing_infrastructure:matter-testing-module",
    "${chip_root}/src/controller/python:matter-core-module",
    "${chip_root}/src/controller/python:matter-clusters-module",
    "${chip_root}/src/controller/python:matter-repl-module",
  ]

  generate_setup_cfg = {
    name = "matter_iot"
    version = "1.5.0.dev1"
    include_default_pyproject_file = true
    include_extra_files_in_package_data = true
    metadata = {
      description = "Matter IoT python development controller and test package"
      url = "https://github.com/project-chip/connectedhomeip"
      classifiers = [
        "Intended Audience :: Developers",
        "License :: OSI Approved :: Apache Software License",
        "Programming Language :: Python :: 3",
      ]
    }
  }

  if (chip_support_commissioning_in_controller) {
    lib_name = "_ChipDeviceCtrl.so"
  } else {
    lib_name = "_ChipServer.so"
  }

  public_deps = [ "${chip_root}/src/controller/python:ChipDeviceCtrl" ]
  public_deps += [
    "${chip_root}/src/python_testing/matter_testing_infrastructure:data_model_zip_1_2",
    "${chip_root}/src/python_testing/matter_testing_infrastructure:data_model_zip_1_3",
    "${chip_root}/src/python_testing/matter_testing_infrastructure:data_model_zip_1_4",
    "${chip_root}/src/python_testing/matter_testing_infrastructure:data_model_zip_1_4_1",
    "${chip_root}/src/python_testing/matter_testing_infrastructure:data_model_zip_1_4_2",
    "${chip_root}/src/python_testing/matter_testing_infrastructure:data_model_zip_1_5",
  ]

  extra_files = [
    "${root_out_dir}/data_model/zip_1_2.zip > matter/testing/data_model/1.2/allfiles.zip",
    "${root_out_dir}/data_model/zip_1_3.zip > matter/testing/data_model/1.3/allfiles.zip",
    "${root_out_dir}/data_model/zip_1_4.zip > matter/testing/data_model/1.4/allfiles.zip",
    "${root_out_dir}/data_model/zip_1_4_1.zip > matter/testing/data_model/1.4.1/allfiles.zip",
    "${root_out_dir}/data_model/zip_1_4_2.zip > matter/testing/data_model/1.4.2/allfiles.zip",
    "${root_out_dir}/data_model/zip_1_5.zip > matter/testing/data_model/1.5/allfiles.zip",
    "${root_out_dir}/obj/src/controller/python/matter/${lib_name} > matter/${lib_name}",
  ]
}

pw_python_wheels("python_wheel_dist") {
  packages = [ ":matter_iot" ]
  directory = "$root_out_dir/dist"
}
